! S. Fartoukh: 06/06/2009: Version compatible with beam1, beam2 or beam4;
! Revised version for SLHCV2: S. Fartoukh 30.09.2009
! Based on SLHCV3.0/removeinstall.madx
! 2011/11 R. De Maria
! To be called once at the beginning of the script
! Use removeinstall.madx to place and displace elements
! - New name conventions and element classes
! - Additional orbit correctors for closed crossing scheme
! - Movable D2,Q4,Q5,Q6
! - MQYL in Q5 IR6
! - Addition MS in Q10
! - Default reference radius
! - Additional corrector package
! 2012/03/19 new MCBX lengths and position, new BPMS position
!            bug in a5/b5 kmax corrected
! 2012/03/20 new name for BPM in the triplet from BPMSD to BPMSQ
!            xywab(d)e already taken
! 2012/03/20 a5/b5 bug in the definition of the strengths
! 2012/07/13 Crab cavity elements as tkickers
! 2016/08/23 Updated layout after rebaselining



!-------------------------------------------------------------------
! Remove install old and new elements
! Triplets, interconnection lenghts and MS postions are defined
! by the calling program
! -----------------------------------------------------------

! mylhcbeam=4 for Beam 4
if (mylhcbeam>2){ bv_aux=-1; } else { bv_aux=1; };


if (on_layout_custom==0){
l.MQXFA     =          4.2000000000 ;
l.MQXFB     =          7.1500000000 ;
dq1aq1b     =          0.6060000000 -0.014; !-0.04 from V1.2
dq1q2a      =          3.6280000000 +0.005 ;
dq2aq2b     =          2.1080000000 +0.004;
dq2bq3      =          3.6280000000 +0.005;
deltaposD2  =        -15.805;!max. field D2 35.1 Tm
deltaposQ4  =         10.500   ;
deltaposQ5  =         10.500 ;
deltaposQ6  =          0.0000000000 ;
deltaposCrab=                     0 ;
deltaposMCBRD=                    0 ;
deltaposTAXN =                    0 ;
on_cutMS.10f =          0            ;
on_cutMS.14f =          0            ;
on_cutMS.10d =          0            ;
on_cutMS.14d =          0            ;
l.MBXF      =          6.27         ;
l.MCBXFB    =          1.2          ;
l.MCBXFA    =          2.2          ;
l.MBRD      =          7.778        ;
l.MCBRD     =          1.930         ;
};

!value, l.MQXL,l.MQX,dq1q2a,dq2aq2b,dq2bq3;
!value, deltaposD2;
!value, deltaposQ4;
!value, deltaposQ5;
!value, deltaposQ6;

!===============================================
! REMOVE CURRENT INSERTION (from TAS to Q6)
!===============================================
if(bv_aux ==1){
   seqedit,sequence=lhcb1;flatten;endedit;
 };
seqedit,sequence=lhcb2;flatten;endedit;
brho = 23348.89927;

removemarker: marker;
marks.l1: removemarker; marke.l1: removemarker;
marks.l5: removemarker; marke.l5: removemarker;
marks.r1: removemarker; marke.r1: removemarker;
marks.r5: removemarker; marke.r5: removemarker;

posmarke=15.0;
!posmarks=163.0; ! include D2
!posmarks=184;   ! include Q4
!posmarks=240;    ! include Q6
posmarks=259;    ! include Q6 DFBAJ DFBAI

if(bv_aux == 1){
        seqedit, sequence=lhcb1;
        install,element=marks.l1,class=removemarker,at=-posmarks,from=IP1.L1;
        install,element=marke.l1,class=removemarker,at=-posmarke,from=IP1.L1;
        install,element=marke.r1,class=removemarker,at= posmarke,from=IP1;
        install,element=marks.r1,class=removemarker,at= posmarks,from=IP1;
        install,element=marks.l5,class=removemarker,at=-posmarks,from=IP5;
        install,element=marke.l5,class=removemarker,at=-posmarke,from=IP5;
        install,element=marke.r5,class=removemarker,at= posmarke,from=IP5;
        install,element=marks.r5,class=removemarker,at= posmarks,from=IP5;
        endedit;
        seqedit, sequence=lhcb1;
        select, flag=seqedit, clear;
        select, flag=seqedit, range=marks.l1/marke.l1;
        select, flag=seqedit, range=marke.r1/marks.r1;
        select, flag=seqedit, range=marks.l5/marke.l5;
        select, flag=seqedit, range=marke.r5/marks.r5;
        remove, element=selected;
        endedit;
        seqedit, sequence=lhcb1;
        select, flag=seqedit, clear;
        select, flag=seqedit, class=BRANA;
        select, flag=seqedit, class=X1FCL;
        select, flag=seqedit, class=X1FCR;
        select, flag=seqedit, class=X1ZDC001;
        select, flag=seqedit, class=X5ZDC001;
        select, flag=seqedit, class=X5ZDC002;
        remove, element=selected;
        endedit;
        seqedit,sequence=lhcb1;
        remove, element=MCBCH.10L1.B1;
        remove, element=MCBCV.10R1.B1;
        remove, element=MCBCH.10L5.B1;
        remove, element=MCBCV.10R5.B1;
        remove, element=MS.10L1.B1;
        remove, element=MS.10R1.B1;
        remove, element=MS.10L5.B1;
        remove, element=MS.10R5.B1;
        remove, element=MCBH.10L1.B1;
        remove, element=MCBV.10R1.B1;
        remove, element=MCBH.10L5.B1;
        remove, element=MCBV.10R5.B1;
        endedit;
};

seqedit, sequence=lhcb2;
install,element=marks.l1,class=removemarker,at=-posmarks*bv_aux,from=IP1.L1;
install,element=marke.l1,class=removemarker,at=-posmarke*bv_aux,from=IP1.L1;
install,element=marke.r1,class=removemarker,at= posmarke*bv_aux,from=IP1;
install,element=marks.r1,class=removemarker,at= posmarks*bv_aux,from=IP1;
install,element=marks.l5,class=removemarker,at=-posmarks*bv_aux,from=IP5;
install,element=marke.l5,class=removemarker,at=-posmarke*bv_aux,from=IP5;
install,element=marke.r5,class=removemarker,at= posmarke*bv_aux,from=IP5;
install,element=marks.r5,class=removemarker,at= posmarks*bv_aux,from=IP5;
endedit;


if(bv_aux == 1){
        seqedit, sequence=lhcb2;
        select, flag=seqedit, clear;
        select, flag=seqedit, range=marks.l1/marke.l1;
        select, flag=seqedit, range=marke.r1/marks.r1;
        select, flag=seqedit, range=marks.l5/marke.l5;
        select, flag=seqedit, range=marke.r5/marks.r5;
        remove, element=selected;
        endedit;
} else {
        seqedit, sequence=lhcb2;
        select, flag=seqedit, clear;
        select, flag=seqedit, range=marke.l1/marks.l1;
        select, flag=seqedit, range=marks.r1/marke.r1;
        select, flag=seqedit, range=marke.l5/marks.l5;
        select, flag=seqedit, range=marks.r5/marke.r5;
        remove, element=selected;
        endedit;
};



seqedit, sequence=lhcb2;
select, flag=seqedit, clear;
select, flag=seqedit, class=BRANA;
select, flag=seqedit, class=X1FCL;
select, flag=seqedit, class=X1FCR;
select, flag=seqedit, class=X1ZDC001;
select, flag=seqedit, class=X5ZDC001;
select, flag=seqedit, class=X5ZDC002;
remove, element=selected;
endedit;

! remove old Q10 corrector package



seqedit,sequence=lhcb2;
remove, element=MCBCV.10L1.B2;
remove, element=MCBCH.10R1.B2;
remove, element=MCBCV.10L5.B2;
remove, element=MCBCH.10R5.B2;
remove, element=MS.10L1.B2;
remove, element=MS.10R1.B2;
remove, element=MS.10L5.B2;
remove, element=MS.10R5.B2;
remove, element=MCBV.10L1.B2;
remove, element=MCBH.10R1.B2;
remove, element=MCBV.10L5.B2;
remove, element=MCBH.10R5.B2;
endedit;


! ===============================================
! macros for installation
! ===============================================

install_ir15_cmn(ENAME,SPOS): macro={
        value,SPOS;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAMEL1,at=-SPOS,from=IP1.L1;
                install, element=ENAMER1,at= SPOS,from=IP1;
                install, element=ENAMEL5,at=-SPOS,from=IP5;
                install, element=ENAMER5,at= SPOS,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAMEL1,at=-SPOS *bv_aux,from=IP1.L1;
        install, element=ENAMER1,at= SPOS *bv_aux,from=IP1;
        install, element=ENAMEL5,at=-SPOS *bv_aux,from=IP5;
        install, element=ENAMER5,at= SPOS *bv_aux,from=IP5;
        endedit;
};

install_ir15_b12(ENAME,SPOS): macro={
    value,SPOS;
    if(bv_aux == 1){
       seqedit,sequence=lhcb1;
       install, element=ENAMEL1.B1,at=-SPOS,from=IP1.L1;
       install, element=ENAMER1.B1,at= SPOS,from=IP1;
       install, element=ENAMEL5.B1,at=-SPOS,from=IP5;
       install, element=ENAMER5.B1,at= SPOS,from=IP5;
       endedit;
    };
    seqedit,sequence=lhcb2;
    install, element=ENAMEL1.B2,at=-SPOS*bv_aux,from=IP1.L1;
    install, element=ENAMER1.B2,at= SPOS*bv_aux,from=IP1;
    install, element=ENAMEL5.B2,at=-SPOS*bv_aux,from=IP5;
    install, element=ENAMER5.B2,at= SPOS*bv_aux,from=IP5;
    endedit;
};

install_ir15_b1_b2(ENAME,SPOS1,SPOS2): macro={
! position b1: spos1 - IP1 - spos1  spos1 - IP5 - spos1
! position b2: spos2 - IP1 - spos2  spos2 - IP5 - spos2
        value,SPOS1,SPOS2;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAMEL1.B1,at=-SPOS1,from=IP1.L1;
                install, element=ENAMER1.B1,at= SPOS1,from=IP1;
                install, element=ENAMEL5.B1,at=-SPOS1,from=IP5;
                install, element=ENAMER5.B1,at= SPOS1,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAMEL1.B2,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAMER1.B2,at= SPOS2*bv_aux,from=IP1;
        install, element=ENAMEL5.B2,at=-SPOS2*bv_aux,from=IP5;
        install, element=ENAMER5.B2,at= SPOS2*bv_aux,from=IP5;
        endedit;
};

install_ir15_b1_b2_inv(ENAME,SPOS1,SPOS2): macro={
! position b1: spos2 - IP1 - spos1  spos1 - IP5 - spos2
! position b2: spos2 - IP1 - spos1  spos1 - IP5 - spos2
        value,SPOS1,SPOS2;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAMEL1.B1,at=-SPOS2,from=IP1.L1;
                install, element=ENAMER1.B1,at= SPOS1,from=IP1;
                install, element=ENAMEL5.B1,at=-SPOS1,from=IP5;
                install, element=ENAMER5.B1,at= SPOS2,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAMEL1.B2,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAMER1.B2,at= SPOS1*bv_aux,from=IP1;
        install, element=ENAMEL5.B2,at=-SPOS1*bv_aux,from=IP5;
        install, element=ENAMER5.B2,at= SPOS2*bv_aux,from=IP5;
        endedit;
};

install_ir15_b1_b2_alt(ENAME,SPOS1,SPOS2): macro={
! position b1: spos1 - IP1 - spos2  spos1 - IP5 - spos2
! position b2: spos2 - IP1 - spos1  spos2 - IP5 - spos1
        value,SPOS1,SPOS2;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAMEL1.B1,at=-SPOS1,from=IP1.L1;
                install, element=ENAMER1.B1,at= SPOS2,from=IP1;
                install, element=ENAMEL5.B1,at=-SPOS1,from=IP5;
                install, element=ENAMER5.B1,at= SPOS2,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAMEL1.B2,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAMER1.B2,at= SPOS1*bv_aux,from=IP1;
        install, element=ENAMEL5.B2,at=-SPOS2*bv_aux,from=IP5;
        install, element=ENAMER5.B2,at= SPOS1*bv_aux,from=IP5;
        endedit;
};

install_ir15_b1_b2_alt2(ENAME1,ENAME2,SPOS1,SPOS2): macro={
! position b1: spos1 - IP1 - spos2  spos1 - IP5 - spos2
! position b2: spos2 - IP1 - spos1  spos2 - IP5 - spos1
        value,SPOS1,SPOS2;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAME1L1.B1,at=-SPOS1,from=IP1.L1;
                install, element=ENAME2R1.B1,at= SPOS2,from=IP1;
                install, element=ENAME1L5.B1,at=-SPOS1,from=IP5;
                install, element=ENAME2R5.B1,at= SPOS2,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAME2L1.B2,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAME1R1.B2,at= SPOS1*bv_aux,from=IP1;
        install, element=ENAME2L5.B2,at=-SPOS2*bv_aux,from=IP5;
        install, element=ENAME1R5.B2,at= SPOS1*bv_aux,from=IP5;
        endedit;
};

install_ir15_b1_b2_alt4(ENAME1,ENAME2,SPOS1,SPOS2,SPOS3,SPOS4): macro={
! position b1: spos1 - IP1 - spos1  spos3 - IP5 - spos3
! position b2: spos2 - IP1 - spos2  spos4 - IP5 - spos4
        value,SPOS1,SPOS2;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAME1L1.B1,at=-SPOS1,from=IP1.L1;
                install, element=ENAME2R1.B1,at= SPOS1,from=IP1;
                install, element=ENAME1L5.B1,at=-SPOS3,from=IP5;
                install, element=ENAME2R5.B1,at= SPOS3,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAME2L1.B2,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAME1R1.B2,at= SPOS2*bv_aux,from=IP1;
        install, element=ENAME2L5.B2,at=-SPOS4*bv_aux,from=IP5;
        install, element=ENAME1R5.B2,at= SPOS4*bv_aux,from=IP5;
        endedit;
};



install_ir15_cmn4(ENAME,SPOS1,SPOS2,SPOS3,SPOS4): macro={
        value,SPOS1,SPOS2,SPOS3,SPOS4;
        if(bv_aux == 1){
                seqedit,sequence=lhcb1;
                install, element=ENAMEL1,at=-SPOS1,from=IP1.L1;
                install, element=ENAMER1,at= SPOS2,from=IP1;
                install, element=ENAMEL5,at=-SPOS3,from=IP5;
                install, element=ENAMER5,at= SPOS4,from=IP5;
                endedit;
        };
        seqedit,sequence=lhcb2;
        install, element=ENAMEL1,at=-SPOS2*bv_aux,from=IP1.L1;
        install, element=ENAMER1,at= SPOS1*bv_aux,from=IP1;
        install, element=ENAMEL5,at=-SPOS4*bv_aux,from=IP5;
        install, element=ENAMER5,at= SPOS3*bv_aux,from=IP5;
        endedit;
};

newel_ir15_b12(ENAME,PARE,MECHSEP): macro={
        ENAMEL1.B1: PARE, mech_sep=-MECHSEP;
        ENAMER1.B1: PARE, mech_sep= MECHSEP;
        ENAMEL5.B1: PARE, mech_sep=-MECHSEP;
        ENAMER5.B1: PARE, mech_sep= MECHSEP;
        ENAMEL1.B2: PARE, mech_sep= MECHSEP*bv_aux;
        ENAMER1.B2: PARE, mech_sep=-MECHSEP*bv_aux;
        ENAMEL5.B2: PARE, mech_sep= MECHSEP*bv_aux;
        ENAMER5.B2: PARE, mech_sep=-MECHSEP*bv_aux;
};




!===============================================
! TAS
!===============================================
!posTAXS  =19.950;
posTAXSL1  =19.979;
posTAXSR1  =19.971;
posTAXSL5  =19.950;
posTAXSR5  =19.950;


l.TAXS  =1.8;

TAXS: COLLIMATOR,  L := l.TAXS; ! new TAS

TAXS.1R1: TAXS; TAXS.1L1: TAXS;
TAXS.1R5: TAXS; TAXS.1L5: TAXS;

! TAS
exec,install_ir15_cmn4(TAXS.1,posTAXSL1,posTAXSR1,posTAXSL5,posTAXSR5);


!===============================================
! Triplet region
!===============================================

! Lstar {.46 |Q1|}
Lstar   =23.00-0.08+0.007;  !22.965 m in V6.503

posQ1a:=Lstar +l.MQXFA/2.;
posQ1b:=posQ1a+l.MQXFA+dq1aq1b;
posQ2a:=posQ1b+(l.MQXFA+l.MQXFB)/2.+dq1q2a;
posQ2b:=posQ2a+l.MQXFB+dq2aq2b;
posQ3a:=posQ2b+(l.MQXFA+l.MQXFB)/2.+dq2bq3;
posQ3b:=posQ3a+l.MQXFA+dq1aq1b;

Kmax_MQXFA=132.6;
Kmax_MQXFB=132.6;
Kmin_MQXFA=132.6*0.03;
Kmin_MQXFB=132.6*0.03;
Imax_MQXFA=16470;
Imax_MQXFB=16470;


! new element types
MQXFA:   QUADRUPOLE, L := l.MQXFA,   ! Q1 Q3
         Kmax:= Kmax_MQXFA, Kmin:= Kmax_MQXFA, Calib=Kmax_MQXFA/Imax_MQXFA;
MQXFB:   QUADRUPOLE, L := l.MQXFB,   ! Q2a Q2b
         Kmax:= Kmax_MQXFB, Kmin:= Kmax_MQXFB, Calib=Kmax_MQXFB/Imax_MQXFB;

! triplet quad strengths in IR1 & IR5

MQXFA.A1L1 : MQXFA,k1:= kqx1.l1  *bv_aux;
MQXFA.A1R1 : MQXFA,k1:= kqx1.r1  *bv_aux;
MQXFA.B1L1 : MQXFA,k1:= kqx1.l1  *bv_aux;
MQXFA.B1R1 : MQXFA,k1:= kqx1.r1  *bv_aux;
MQXFB.A2L1 : MQXFB,k1:=-kqx2a.l1 *bv_aux;
MQXFB.B2L1 : MQXFB,k1:=-kqx2b.l1 *bv_aux;
MQXFB.A2R1 : MQXFB,k1:=-kqx2a.r1 *bv_aux;
MQXFB.B2R1 : MQXFB,k1:=-kqx2b.r1 *bv_aux;
MQXFA.A3L1 : MQXFA,k1:= kqx3.l1  *bv_aux;
MQXFA.A3R1 : MQXFA,k1:= kqx3.r1  *bv_aux;
MQXFA.B3L1 : MQXFA,k1:= kqx3.l1  *bv_aux;
MQXFA.B3R1 : MQXFA,k1:= kqx3.r1  *bv_aux;

MQXFA.A1L5 : MQXFA,k1:= kqx1.l5  *bv_aux;
MQXFA.A1R5 : MQXFA,k1:= kqx1.r5  *bv_aux;
MQXFA.B1L5 : MQXFA,k1:= kqx1.l5  *bv_aux;
MQXFA.B1R5 : MQXFA,k1:= kqx1.r5  *bv_aux;
MQXFB.A2L5 : MQXFB,k1:=-kqx2a.l5 *bv_aux;
MQXFB.B2L5 : MQXFB,k1:=-kqx2b.l5 *bv_aux;
MQXFB.A2R5 : MQXFB,k1:=-kqx2a.r5 *bv_aux;
MQXFB.B2R5 : MQXFB,k1:=-kqx2b.r5 *bv_aux;
MQXFA.A3L5 : MQXFA,k1:= kqx3.l5  *bv_aux;
MQXFA.A3R5 : MQXFA,k1:= kqx3.r5  *bv_aux;
MQXFA.B3L5 : MQXFA,k1:= kqx3.l5  *bv_aux;
MQXFA.B3R5 : MQXFA,k1:= kqx3.r5  *bv_aux;

! Triplet
exec,install_ir15_cmn(MQXFA.A1,    posQ1a  );
exec,install_ir15_cmn(MQXFA.B1,    posQ1b  );
exec,install_ir15_cmn(MQXFB.A2,    posQ2A  );
exec,install_ir15_cmn(MQXFB.B2,    posQ2B  );
exec,install_ir15_cmn(MQXFA.A3,    posQ3a  );
exec,install_ir15_cmn(MQXFA.B3,    posQ3b  );

!===============================================
! MCBX in Q2 (small orbit corrector)
!===============================================

! .488 should become 478 and 479 to be checked...

! MCBX
! Q1  |.5 } 1 {.5 | MCBXFBa | .488 | Q2a
! Q2a |.5 } 1 {.5 | Q2b | .489| MCBXFBb |.5 }
! Q2b |.489 | MCBXFBb |.1 } .1 {.5 | Q3 | .5}

posMCBXFBa  := posQ2a     -(l.MQXFB + l.MCBXFB   )/2. -0.464; !0.498 TBC
posMCBXFBb  := posQ2b     +(l.MQXFB + l.MCBXFB   )/2. +0.464; !0.498 TBC
!value,l.MCBXFB, posMCBXFBa,posMCBXFBb;

! Cabling orbit corrector inside Q2a and Q2b cold masses

MCBXFBH: HKICKER,    Lrad := l.MCBXFB;  ! nested corrector
MCBXFBV: VKICKER,    Lrad := l.MCBXFB;  ! nested corrector

Kmax_MCBXFB := 2.5000 / l.MCBXFB; ! 1.8Tm integrated strength
Kmin_MCBXFB := -Kmax_MCBXFB ;
value, Kmax_MCBXFB;

MCBXFBH,  Kmax:=Kmax_MCBXFB, Kmin:=Kmin_MCBXFB;
MCBXFBV,  Kmax:=Kmax_MCBXFB, Kmin:=Kmin_MCBXFB;


MCBXFBH.a2L1: MCBXFBH, KICK := acbxh1.l1;
MCBXFBH.a2R1: MCBXFBH, KICK := acbxh1.r1;
MCBXFBH.a2L5: MCBXFBH, KICK := acbxh1.l5;
MCBXFBH.a2R5: MCBXFBH, KICK := acbxh1.r5;
MCBXFBV.a2L1: MCBXFBV, KICK := acbxv1.l1*bv_aux;
MCBXFBV.a2R1: MCBXFBV, KICK := acbxv1.r1*bv_aux;
MCBXFBV.a2L5: MCBXFBV, KICK := acbxv1.l5*bv_aux;
MCBXFBV.a2R5: MCBXFBV, KICK := acbxv1.r5*bv_aux;
MCBXFBH.b2L1: MCBXFBH, KICK := acbxh2.l1;
MCBXFBH.b2R1: MCBXFBH, KICK := acbxh2.r1;
MCBXFBH.b2L5: MCBXFBH, KICK := acbxh2.l5;
MCBXFBH.b2R5: MCBXFBH, KICK := acbxh2.r5;
MCBXFBV.b2L1: MCBXFBV, KICK := acbxv2.l1*bv_aux;
MCBXFBV.b2R1: MCBXFBV, KICK := acbxv2.r1*bv_aux;
MCBXFBV.b2L5: MCBXFBV, KICK := acbxv2.l5*bv_aux;
MCBXFBV.b2R5: MCBXFBV, KICK := acbxv2.r5*bv_aux;

! Orbit correctors
exec,install_ir15_cmn(MCBXFBH.A2,  posMCBXFBa );
exec,install_ir15_cmn(MCBXFBV.A2,  posMCBXFBa );
exec,install_ir15_cmn(MCBXFBH.B2,  posMCBXFBb );
exec,install_ir15_cmn(MCBXFBV.B2,  posMCBXFBb );



!===============================================
!Corrector package
!===============================================

! layout

                         !V1.3  V1.2
l.MQSXF   = 0.402 ;  !0.807 0.716 a2 corrector
l.MCSXF   = 0.168 ;  !0.111 0.121 b3 corrector
l.MCSSXF  = 0.168 ;  !0.111 0.121 a3 corrector
l.MCOXF   = 0.145 ;  !0.087 0.137 b4 corrector
l.MCOSXF  = 0.145 ;  !0.087 0.137 a4 corrector
l.MCDXF   = 0.145 ;  !0.095 0.079 b5 corrector
l.MCDSXF  = 0.145 ;  !0.095 0.079 a5 corrector
l.MCTXF   = 0.469 ;  !0.430 0.339 b6 corrector
l.MCTSXF  = 0.099;  !0.089 0.087 a6 corrector


! Q3|.5} 1.00 {.5  | MCBXFA  | .5
!              .08 | MQSXF  | .08
!              .08 | MCTXF   | .08
!              .08 | MCTSXF  | .08
!              .08 | MCDXF   | .08
!              .08 | MCDSXF  | .08
!              .08 | MCOXF   | .08
!              .08 | MCOSXF  | .08
!              .08 | MCSXF   | .08
!              .08 | MCSSXF  | .08 }

posMCBXFA  := posQ3b     + (l.MQXFA   + l.MCBXFA )/2.+2.727; !68.560
posMQSXF   := posMCBXFA  + 1.61; ! 70.17
posMCTXF   := posMQSXF   + 0.64; !value,posMCTXF -70.81;
posMCTSXF  := posMCTXF   + 0.438; !value,posMCTSXF-71.248;
posMCDXF   := posMCTSXF  + 0.271; !value,posMCDXF -71.518;
posMCDSXF  := posMCDXF   + 0.29; !value,posMCDSXF-71.808;
posMCOXF   := posMCDSXF  + 0.29; !value,posMCOXF -72.0990;
posMCOSXF  := posMCOXF   + 0.29; !value,posMCOSXF-72.3890;
posMCSXF   := posMCOSXF  + 0.299; !value,posMCSXF -72.6880;
posMCSSXF  := posMCSXF   + 0.308; !value,posMCSSXF-72.9960;

value,posMCBXFA,posMQSXF,posMCTXF,posMCTSXF,posMCDXF,posMCDSXF,posMCOXF,posMCOSXF,posMCSXF,posMCSSXF;

! orbit corrector elements
MCBXFAH : HKICKER   , Lrad := l.MCBXFA  ; ! H in corrector package (long for crabs)
MCBXFAV : VKICKER   , Lrad := l.MCBXFA  ; ! V in corrector package (long for crabs)
MQSXF   : QUADRUPOLE, L    := l.MQSXF   ; ! (a2)
MCTXF   : MULTIPOLE , Lrad := l.MCTXF   ; ! (b6)
MCTSXF  : MULTIPOLE , Lrad := l.MCTSXF  ; ! (a6)
MCDXF   : MULTIPOLE , Lrad := l.MCDXF   ; ! (b5)
MCDSXF  : MULTIPOLE , Lrad := l.MCDSXF  ; ! (a5)
MCOXF   : MULTIPOLE , Lrad := l.MCOXF   ; ! (b4)
MCOSXF  : MULTIPOLE , Lrad := l.MCOSXF  ; ! (a4)
MCSXF   : MULTIPOLE , Lrad := l.MCSXF   ; ! (b3)
MCSSXF  : MULTIPOLE , Lrad := l.MCSSXF  ; ! (a3)


Kmax_MCBXFA:= 4.5000 / l.MCBXFA                     ;!4.5000Tm@50mm
Rref_mpcorr = 0.05; !50mm reference radius for multipole correctors
Kmax_MQSXF := 0.7000 / l.MQSXF/Rref_mpcorr          ;!1.0000Tm@50mm
Kmax_MCSXF := 0.0950 / l.MCSXF /Rref_mpcorr^2 *   2.;!0.0630Tm@50mm
Kmax_MCSSXF:= 0.0950 / l.MCSSXF/Rref_mpcorr^2 *   2.;!0.0630Tm@50mm
Kmax_MCOXF := 0.0690 / l.MCOXF /Rref_mpcorr^3 *   6.;!0.0460Tm@50mm
Kmax_MCOSXF:= 0.0690 / l.MCOSXF/Rref_mpcorr^3 *   6.;!0.0460Tm@50mm
Kmax_MCDXF := 0.0370 / l.MCDXF /Rref_mpcorr^4 *  24.;!0.0250Tm@50mm
Kmax_MCDSXF:= 0.0370 / l.MCDSXF/Rref_mpcorr^4 *  24.;!0.0250Tm@50mm
Kmax_MCTXF := 0.0860 / l.MCTXF /Rref_mpcorr^5 * 120.;!0.0860Tm@50mm
Kmax_MCTSXF:= 0.0170 / l.MCTSXF/Rref_mpcorr^5 * 120.;!0.0170Tm@50mm


Kmin_MCBXFA:= -Kmax_MCBXFA ;
Kmin_MQSXF := -Kmax_MQSXF ;
Kmin_MCSXF := -Kmax_MCSXF ;
Kmin_MCSSXF:= -Kmax_MCSSXF;
Kmin_MCOXF := -Kmax_MCOXF ;
Kmin_MCOSXF:= -Kmax_MCOSXF;
Kmin_MCTXF := -Kmax_MCTXF ;
Kmin_MCDXF := -Kmax_MCDXF ;
Kmin_MCDSXF:= -Kmax_MCDSXF;
Kmin_MCTSXF:= -Kmax_MCTSXF;

Imax_MCBXFA =1584; !I 1402 (0)
Imax_MCBXFB =1625; !I 1474 (O)
Imax_MQSXF  = 174;
Imax_MCSSXF =  99;
Imax_MCSXF  =  99;
Imax_MCOSXF = 102;
Imax_MCOXF  = 102;
Imax_MCDSXF =  92;
Imax_MCDXF  =  92;
Imax_MCTXF  =  85;
Imax_MCTSXF =  84;

! cabling
MCBXFAH, Kmax:=Kmax_MCBXFA, Kmin:=Kmin_MCBXFA;
MCBXFAV, Kmax:=Kmax_MCBXFA, Kmin:=Kmin_MCBXFA;
MCBXFAH.3L1: MCBXFAH, KICK := acbxh3.l1;
MCBXFAH.3R1: MCBXFAH, KICK := acbxh3.r1;
MCBXFAH.3L5: MCBXFAH, KICK := acbxh3.l5;
MCBXFAH.3R5: MCBXFAH, KICK := acbxh3.r5;
MCBXFAV.3L1: MCBXFAV, KICK := acbxv3.l1*bv_aux;
MCBXFAV.3R1: MCBXFAV, KICK := acbxv3.r1*bv_aux;
MCBXFAV.3L5: MCBXFAV, KICK := acbxv3.l5*bv_aux;
MCBXFAV.3R5: MCBXFAV, KICK := acbxv3.r5*bv_aux;


!A2 CORRECTOR
MQSXF, Kmax:=Kmax_MQSXF , Kmin:=Kmin_MQSXF;
MQSXF.3L1: MQSXF, K1S:=kqsx3.l1;
MQSXF.3R1: MQSXF, K1S:=kqsx3.r1;
MQSXF.3L5: MQSXF, K1S:=kqsx3.l5;
MQSXF.3R5: MQSXF, K1S:=kqsx3.r5;


!B3 CORRECTOR
MCSXF, Kmax:=Kmax_MCSXF , Kmin:=Kmin_MCSXF;
MCSXF.3L1: MCSXF, KNL := {0, 0, kcsx3.l1*l.MCSXF};
MCSXF.3R1: MCSXF, KNL := {0, 0, kcsx3.r1*l.MCSXF};
MCSXF.3L5: MCSXF, KNL := {0, 0, kcsx3.l5*l.MCSXF};
MCSXF.3R5: MCSXF, KNL := {0, 0, kcsx3.r5*l.MCSXF};

!B6 CORRECTOR
MCTXF, Kmax:=Kmax_MCTXF , Kmin:=Kmin_MCTXF;
MCTXF.3L1: MCTXF, KNL := {0, 0, 0, 0, 0, kctx3.l1 *bv_aux*l.MCTXF};
MCTXF.3R1: MCTXF, KNL := {0, 0, 0, 0, 0, kctx3.r1 *bv_aux*l.MCTXF};
MCTXF.3L5: MCTXF, KNL := {0, 0, 0, 0, 0, kctx3.l5 *bv_aux*l.MCTXF};
MCTXF.3R5: MCTXF, KNL := {0, 0, 0, 0, 0, kctx3.r5 *bv_aux*l.MCTXF};

!A6 CORRECTOR
MCTSXF, Kmax:=Kmax_MCTSXF , Kmin:=Kmin_MCTSXF;
MCTSXF.3L1: MCTSXF, KSL := {0, 0, 0, 0, 0, kctsx3.l1 *l.MCTSXF};
MCTSXF.3R1: MCTSXF, KSL := {0, 0, 0, 0, 0, kctsx3.r1 *l.MCTSXF};
MCTSXF.3L5: MCTSXF, KSL := {0, 0, 0, 0, 0, kctsx3.l5 *l.MCTSXF};
MCTSXF.3R5: MCTSXF, KSL := {0, 0, 0, 0, 0, kctsx3.r5 *l.MCTSXF};

!A3 CORRECTOR
MCSSXF, Kmax:=Kmax_MCSSXF, Kmin:=Kmin_MCSSXF;
MCSSXF.3L1: MCSSXF, KSL := {0, 0, kcssx3.l1 *bv_aux*l.MCSSXF};
MCSSXF.3R1: MCSSXF, KSL := {0, 0, kcssx3.r1 *bv_aux*l.MCSSXF};
MCSSXF.3L5: MCSSXF, KSL := {0, 0, kcssx3.l5 *bv_aux*l.MCSSXF};
MCSSXF.3R5: MCSSXF, KSL := {0, 0, kcssx3.r5 *bv_aux*l.MCSSXF};

!A4 CORRECTOR
MCOSXF, Kmax:=Kmax_MCOSXF, Kmin:=Kmin_MCOSXF;
MCOSXF.3L1: MCOSXF, KSL := {0, 0, 0, kcosx3.l1*l.MCOSXF};
MCOSXF.3R1: MCOSXF, KSL := {0, 0, 0, kcosx3.r1*l.MCOSXF};
MCOSXF.3L5: MCOSXF, KSL := {0, 0, 0, kcosx3.l5*l.MCOSXF};
MCOSXF.3R5: MCOSXF, KSL := {0, 0, 0, kcosx3.r5*l.MCOSXF};

!B4 CORRECTOR
MCOXF, Kmax:=Kmax_MCOXF , Kmin:=Kmin_MCOXF;
MCOXF.3L1: MCOXF, KNL := {0, 0, 0, kcox3.l1 *bv_aux*l.MCOXF};
MCOXF.3R1: MCOXF, KNL := {0, 0, 0, kcox3.r1 *bv_aux*l.MCOXF};
MCOXF.3L5: MCOXF, KNL := {0, 0, 0, kcox3.l5 *bv_aux*l.MCOXF};
MCOXF.3R5: MCOXF, KNL := {0, 0, 0, kcox3.r5 *bv_aux*l.MCOXF};

!A5 CORRECTOR
MCDSXF, Kmax:=Kmax_MCDSXF, Kmin:=Kmin_MCDSXF;
MCDSXF.3L1: MCDSXF, KSL := {0, 0, 0, 0, kcdsX3.l1 *bv_aux*l.MCDSXF};
MCDSXF.3R1: MCDSXF, KSL := {0, 0, 0, 0, kcdsX3.r1 *bv_aux*l.MCDSXF};
MCDSXF.3L5: MCDSXF, KSL := {0, 0, 0, 0, kcdsX3.l5 *bv_aux*l.MCDSXF};
MCDSXF.3R5: MCDSXF, KSL := {0, 0, 0, 0, kcdsX3.r5 *bv_aux*l.MCDSXF};

!B5 CORRECTOR
MCDXF, Kmax:=Kmax_MCDXF , Kmin:=Kmin_MCDXF;
MCDXF.3L1: MCDXF, KNL := {0, 0, 0, 0, kcdx3.l1*l.MCDXF};
MCDXF.3R1: MCDXF, KNL := {0, 0, 0, 0, kcdx3.r1*l.MCDXF};
MCDXF.3L5: MCDXF, KNL := {0, 0, 0, 0, kcdx3.l5*l.MCDXF};
MCDXF.3R5: MCDXF, KNL := {0, 0, 0, 0, kcdx3.r5*l.MCDXF};



! Installl non linear correctors
exec,install_ir15_cmn(MCBXFAH.3,posMCBXFA); !b1
exec,install_ir15_cmn(MCBXFAV.3,posMCBXFA); !a1
exec,install_ir15_cmn(MQSXF.3,  posMQSXF); !a2
exec,install_ir15_cmn(MCTXF.3,   posMCTXF  ); !b6
exec,install_ir15_cmn(MCTSXF.3,  posMCTSXF ); !a6
exec,install_ir15_cmn(MCDXF.3,   posMCDXF ); !b5
exec,install_ir15_cmn(MCDSXF.3,  posMCDSXF  ); !a5
exec,install_ir15_cmn(MCOXF.3,   posMCOXF ); !b4
exec,install_ir15_cmn(MCOSXF.3,  posMCOSXF  ); !a4
exec,install_ir15_cmn(MCSXF.3,   posMCSXF  ); !b3
exec,install_ir15_cmn(MCSSXF.3,  posMCSSXF ); !a3



!===============================================
! D1 area
!===============================================

! MCSSXF | .5} .1 {.5 D1 .5} 1 DFXJ

! separation recombination dipoles
!l.MBXF= 6.69;
!l.MBXF= 6.36;
!l.MBXF= 6.27;
MBXF : RBEND,   L := l.MBXF ;  ! D1
MBXF.4L1: MBXF,angle:= ad1.lr1, K0 := kd1.lr1, polarity=+1 *bv_aux;
MBXF.4R1: MBXF,angle:=-ad1.lr1, K0 :=-kd1.lr1, polarity=-1 *bv_aux;
MBXF.4L5: MBXF,angle:= ad1.lr5, K0 := kd1.lr5, polarity=+1 *bv_aux;
MBXF.4R5: MBXF,angle:=-ad1.lr5, K0 :=-kd1.lr5, polarity=-1 *bv_aux;
posD1 = posQ3b+(l.MQXFA+l.MBXF)/2+9.81;
! MBXF cryostat ends ay 82.700
! MBXF cold madd ends at 81.467

! service module for current distribution in the non-IP side of D1
l.DFXJ = 3.525;
DFXJ : PLACEHOLDER, Lrad:= l.DFXJ;
posDFXJ := 93.018-l.DFXJ/2; !mid of vacuum modules
DFXJ.4L1: DFXJ; DFXJ.4R1: DFXJ; DFXJ.4L5: DFXJ; DFXJ.4R5: DFXJ;

posVMBXF = 81.826; !end triplet screen
posVMBXFA= 81.836; !start new beam screen
posVMBXFB= 82.386; !end beam screen

VMBXF.4L1: marker; VMBXF.4R1: marker; VMBXF.4L5: marker; VMBXF.4R5: marker;
VMBXFA.4L1: marker; VMBXFA.4R1: marker; VMBXFA.4L5: marker; VMBXFA.4R5: marker;
VMBXFB.4L1: marker; VMBXFB.4R1: marker; VMBXFB.4L5: marker; VMBXFB.4R5: marker;




! D1
exec,install_ir15_cmn(MBXF.4,  posD1);
exec,install_ir15_cmn(DFXJ.4,  posDFXJ);
exec,install_ir15_cmn(VMBXF.4, posVMBXF);
exec,install_ir15_cmn(VMBXFA.4, posVMBXFA);
exec,install_ir15_cmn(VMBXFB.4, posVMBXFB);

!===============================================
!BPM triplet D1
!===============================================
! to be changed
!           Q1  | .5 } .526  BPM .172 {.5 | MCBXFBa
! MCBXFBa | Q2a | .5 } .054  BPM .812 {.5 | Q2b
!       MCBXFBb | .0}  .392  BPM .805 {.5 | Q3
!           Q3  | .5 } .047  BPM ...

! BMPSQW is between TAS and Q1
l.BPMQSTZA  = 0.2; !needs zero lengths BPMs
l.BPMQSTZB  = 0.2; !needs zero lengths BPMs
l.BPMQSTZW = 0.285; !needs zero lengths BPMs
BPMQSTZA:  MONITOR,    Lrad := l.BPMQSTZA;  ! bpm in the triplets Q1
BPMQSTZB:  MONITOR,    Lrad := l.BPMQSTZB;  ! bpm in the triplets Q2-D1
BPMQSTZW:  MONITOR,    Lrad := l.BPMQSTZW;  ! bpm in D1 warm to be removed

posBPM1 := posQ1a - l.MQXFA/2 - 1.074 + 0.018;!BPMSQ.1R5.B1
posBPM2 := posQ1b + l.MQXFA/2 + 1.154 + 0.035;!BPMSQ.A2R5.B1
posBPM3 := posQ2a + l.MQXFB/2 + 1.156 + 0.035;!BPMSQT.B2R5.B1
posBPM4 := posQ3a - l.MQXFA/2 - 0.954 + 0.036;!BPMSQ.A3R5.B1
posBPM5 := posQ3b + l.MQXFA/2 + 1.154 + 0.027;!BPMSQ.B3R5.B1
posBPM6 := posD1  - l.MBXF/2  - 0.702 + 0.026;!BPMSQ.4R5.B1
posBPM7 := posD1  + l.MBXF/2  + 2.985 ; ! mag+cold mass+cryostat+vac+bpm
! posBPM7 83.7265, best location 84.150

value, posBPM1,posBPM2,posBPM3,posBPM4,posBPM5,posBPM6,posBPM7;

! xpos=array([21.853,33.073,43.858,54.643,65.743,87.89])
! c_[abs(a),abs(a-1)].min(axis=1)*3.74
! array([ 0.587,  0.587,  1.022,  1.457,  1.577,  1.87 ])



noSEP = 0;

exec,newel_ir15_b12(BPMQSTZA.1 ,BPMQSTZA,noSEP);
exec,newel_ir15_b12(BPMQSTZB.A2,BPMQSTZB,noSEP);
exec,newel_ir15_b12(BPMQSTZB.B2,BPMQSTZB,noSEP);
exec,newel_ir15_b12(BPMQSTZB.A3,BPMQSTZB,noSEP);
exec,newel_ir15_b12(BPMQSTZB.B3,BPMQSTZB,noSEP);
exec,newel_ir15_b12(BPMQSTZB.4 ,BPMQSTZB,noSEP);
exec,newel_ir15_b12(BPMQSTZW.4 ,BPMQSTZW,noSEP);

! BPMS
exec,install_ir15_b12(BPMQSTZA.1   ,  posBPM1   );
exec,install_ir15_b12(BPMQSTZB.A2  ,  posBPM2   );
exec,install_ir15_b12(BPMQSTZB.B2  ,  posBPM3   );
exec,install_ir15_b12(BPMQSTZB.A3  ,  posBPM4   );
exec,install_ir15_b12(BPMQSTZB.B3  ,  posBPM5   );
exec,install_ir15_b12(BPMQSTZB.4   ,  posBPM6   );
exec,install_ir15_b12(BPMQSTZW.4   ,  posBPM7   );

!===============================================
! D2 area
!===============================================

! D2
!l.MBRD=10;
!if (l.MBRD==0) {l.MBRD=7.8;};
!if (l.MCBRD==0) {l.MCBRD=1.5;};
l.BPMQBCZA=0.285;
l.BPMQBCZB=0.285;
MBRD  : RBEND  ,   L := l.MBRD ;  ! D2
MCBRDH: HKICKER,   L := l.MCBRD; ! long for crabs in D2 cryostats
MCBRDV: VKICKER,   L := l.MCBRD; ! long for crabs in D2 cryostats
BPMQBCZA : MONITOR,   Lrad := l.BPMQBCZA;  ! bpm in D2
BPMQBCZB : MONITOR,   Lrad := l.BPMQBCZB;  ! bpm in D2
VMBRDA: MARKER,   L=0; ! end b.s. IP side
VMBRDB: MARKER,   L=0; ! end b.s. non-IP side

posD2  := 157.9+deltaposD2; ! minimize distance D1-D2, maximum field 35 Tm
!posD2  = 157.9-8-7;     ! from V6.503
! 8 to fit two cavitities with 1.5 meter at 163
!11 to fit three cavitities with 1.5 meter

posMCBRDA  := posD2 + l.MBRD/2 + l.MCBRD/2 + 0.357 + deltaposMCBRD;
posMCBRDB  := posMCBRDA + l.MCBRD          + 0.294 ;

posVMBRDA  :=  posD2 -4.755 + 0.200; !end b.s. IP side
posVMBRDB  :=  posD2 +  10.288 - 0.667; !end b.s. non-IP side
sepVMBRDA  := 0.188; sepVMBRDB  := 0.188;
posBPMQBCZA  := posD2 + 9.723;
posBPMQBCZB  := posD2 + 9.783;
sepBPMWQ  := 0.194; !tentative

VMBRD: marker;

MBRD.4L1.B1: MBRD, angle:=-ad2.l1, K0:=-kd2.l1, polarity=-1, mech_sep=-0.1880;
MBRD.4R1.B1: MBRD, angle:= ad2.r1, K0:= kd2.r1, polarity=+1, mech_sep= 0.1880;
MBRD.4L5.B1: MBRD, angle:=-ad2.l5, K0:=-kd2.l5, polarity=-1, mech_sep=-0.1880;
MBRD.4R5.B1: MBRD, angle:= ad2.r5, K0:= kd2.r5, polarity=+1, mech_sep= 0.1880;

MBRD.4L1.B2: MBRD, angle:=-ad2.l1, K0:=-kd2.l1, polarity=+1*bv_aux, mech_sep= 0.1880*bv_aux;
MBRD.4R1.B2: MBRD, angle:= ad2.r1, K0:= kd2.r1, polarity=-1*bv_aux, mech_sep=-0.1880*bv_aux;
MBRD.4L5.B2: MBRD, angle:=-ad2.l5, K0:=-kd2.l5, polarity=+1*bv_aux, mech_sep= 0.1880*bv_aux;
MBRD.4R5.B2: MBRD, angle:= ad2.r5, K0:= kd2.r5, polarity=-1*bv_aux, mech_sep=-0.1880*bv_aux;

MCBRDH.4L1.B1: MCBRDH, kick:= acbrdh4.l1b1, mech_sep=-0.1880;
MCBRDH.4R1.B1: MCBRDH, kick:= acbrdh4.r1b1, mech_sep= 0.1880;
MCBRDH.4L5.B1: MCBRDH, kick:= acbrdh4.l5b1, mech_sep=-0.1880;
MCBRDH.4R5.B1: MCBRDH, kick:= acbrdh4.r5b1, mech_sep= 0.1880;
MCBRDH.4L1.B2: MCBRDH, kick:=-acbrdh4.l1b2, mech_sep= 0.1880*bv_aux;
MCBRDH.4R1.B2: MCBRDH, kick:=-acbrdh4.r1b2, mech_sep=-0.1880*bv_aux;
MCBRDH.4L5.B2: MCBRDH, kick:=-acbrdh4.l5b2, mech_sep= 0.1880*bv_aux;
MCBRDH.4R5.B2: MCBRDH, kick:=-acbrdh4.r5b2, mech_sep=-0.1880*bv_aux;
MCBRDV.4L1.B1: MCBRDV, kick:= acbrdv4.l1b1*bv_aux, mech_sep=-0.1880;
MCBRDV.4R1.B1: MCBRDV, kick:= acbrdv4.r1b1*bv_aux, mech_sep= 0.1880;
MCBRDV.4L5.B1: MCBRDV, kick:= acbrdv4.l5b1*bv_aux, mech_sep=-0.1880;
MCBRDV.4R5.B1: MCBRDV, kick:= acbrdv4.r5b1*bv_aux, mech_sep= 0.1880;
MCBRDV.4L1.B2: MCBRDV, kick:=-acbrdv4.l1b2*bv_aux, mech_sep= 0.1880*bv_aux;
MCBRDV.4R1.B2: MCBRDV, kick:=-acbrdv4.r1b2*bv_aux, mech_sep=-0.1880*bv_aux;
MCBRDV.4L5.B2: MCBRDV, kick:=-acbrdv4.l5b2*bv_aux, mech_sep= 0.1880*bv_aux;
MCBRDV.4R5.B2: MCBRDV, kick:=-acbrdv4.r5b2*bv_aux, mech_sep=-0.1880*bv_aux;

exec,newel_ir15_b12(VMBRDA.4,VMBRD,sepVMBRDA);
exec,newel_ir15_b12(VMBRDB.4,VMBRD,sepVMBRDB);

BPMQBCZA.4L1.B1: BPMQBCZA, mech_sep=-sepBPMWQ;
BPMQBCZB.4L1.B2: BPMQBCZB, mech_sep= sepBPMWQ*bv_aux;
BPMQBCZB.4R1.B1: BPMQBCZB, mech_sep= sepBPMWQ;
BPMQBCZA.4R1.B2: BPMQBCZA, mech_sep=-sepBPMWQ*bv_aux;
BPMQBCZA.4L5.B1: BPMQBCZA, mech_sep=-sepBPMWQ;
BPMQBCZB.4L5.B2: BPMQBCZB, mech_sep= sepBPMWQ*bv_aux;
BPMQBCZB.4R5.B1: BPMQBCZB, mech_sep= sepBPMWQ;
BPMQBCZA.4R5.B2: BPMQBCZA, mech_sep=-sepBPMWQ*bv_aux;


! separation recombination powering, 0.194 m separation between apertures
ad1.lr1:=atan(0.194/2./(posD2-posD1))*(1-R0);
kd1.lr1:=ad1.lr1/l.MBXF;
ad1.lr5:=atan(0.194/2./(posD2-posD1))*(1-R0);
kd1.lr5:=ad1.lr5/l.MBXF;
ad2.l1 :=atan(0.194/2./(posD2-posD1))*(1-R0);
ad2.r1 :=atan(0.194/2./(posD2-posD1))*(1-R0);
kd2.l1 :=ad2.l1/l.MBRD;
kd2.r1 :=ad2.r1/l.MBRD;
ad2.l5 :=atan(0.194/2./(posD2-posD1))*(1-R0);
ad2.r5 :=atan(0.194/2./(posD2-posD1))*(1-R0);
kd2.l5 :=ad2.l5/l.MBRD;
kd2.r5 :=ad2.r5/l.MBRD;

value, posD1,posD2,posD2-posD1,posMCBRDA,posMCBRDB;
value, posBPMWQ,sepBPMWQ;
value, ad1.lr1,l.MBXF,ad1.lr1/l.MBXF*brho,ad1.lr1*brho;
value, ad2.l1,l.MBRD,ad2.l1/l.MBRD*brho,ad2.l1*brho;

! D2
exec,install_ir15_b12(MBRD.4 , posD2   );
exec,install_ir15_b1_b2_alt2(BPMQBCZA.4, BPMQBCZB.4,posBPMQBCZA,posBPMQBCZB);

! email arnaud 8/3/2019,
! B2  =V||H=    =V||H= B1 (ext)
! B1  =H||V=    =H||V= B2 (int)
! MCB%A(L) MCB%A(R) MCB%A(L) MCB%A(R)
! herve 18/2/2019
! MCB%A(R) = MCB%H ext + MCB%V int
! MCB%B(R) = MCB%V ext + MCB%H int
! MCB%A(L) = MCB%V ext + MCB%H int
! MCB%B(L) = MCB%H ext + MCB%V int

if (cut_mcbrd==0){
!IP - D2 posMCBRDA posMCBRDB
! orientation:
! b1: h v - IP - v h
! b2: v h - IP - h v
exec,install_ir15_b1_b2_alt(MCBRDV.4,posMCBRDA,posMCBRDB);
exec,install_ir15_b1_b2_alt(MCBRDH.4,posMCBRDB,posMCBRDA);
! orientation:
! b1: h v - IP - h v
! b2: v h - IP - v h
!exec,install_ir15_b1_b2_alt(MCBRDV.4,posMCBRDA,posMCBRDB);
!exec,install_ir15_b1_b2_alt(MCBRDH.4,posMCBRDB,posMCBRDA);
};

!===============================================
! Q4
!===============================================

!l.MQYY =3.4+0.1;
!l.MQYY =3.5 3.675
!if (l.MQYY==0) { l.MQYY=3.826; };
!if (l.MCBYY==0 ) {l.MCBYY=1.50;};


posQ4       :=169.553+deltaPosQ4;  !new layout (excel file) 182.644 m

posBPMYA.4 :=posQ4 + l.MQY/2 + 0.974 ;!BPMYA.4R5.B1
dposMCBY.B4 := (l.MQY + l.MCBYV)/2 + 0.372; !same as nominal note that in IR2-8-6 distance can
dposMCBY.4  := dposMCBY.B4 + (l.MCBYH + l.MCBYV)/2 + 0.396; !be better
dposMCBY.A4 := dposMCBY.4  + (l.MCBYV + l.MCBYH)/2 + 0.396;

value,posq4-dposMCBY.B4,posq4-dposMCBY.4,posq4-dposMCBY.A4;




MCBYH.4L1.B1:MCBYH, KICK:=  acbyhs4.l1b1, polarity=+1,mech_sep=-0.194;
MCBYH.4L5.B1:MCBYH, KICK:=  acbyhs4.l5b1, polarity=+1,mech_sep=-0.194;
MCBYH.A4R1.B1:MCBYH, KICK:=  acbyhs4.r1b1, polarity=+1,mech_sep= 0.194;
MCBYH.A4R5.B1:MCBYH, KICK:=  acbyhs4.r5b1, polarity=+1,mech_sep= 0.194;
MCBYH.B4R1.B1:MCBYH, KICK:=  acbyh4.r1b1, polarity=+1,mech_sep= 0.194;
MCBYH.B4R5.B1:MCBYH, KICK:=  acbyh4.r5b1, polarity=+1,mech_sep= 0.194;

MCBYV.4R1.B1:MCBYV, KICK:=  acbyvs4.r1b1, polarity=+1,mech_sep= 0.194;
MCBYV.4R5.B1:MCBYV, KICK:=  acbyvs4.r5b1, polarity=+1,mech_sep= 0.194;
MCBYV.A4L1.B1:MCBYV, KICK:=  acbyvs4.l1b1, polarity=+1,mech_sep=-0.194;
MCBYV.A4L5.B1:MCBYV, KICK:=  acbyvs4.l5b1, polarity=+1,mech_sep=-0.194;
MCBYV.B4L1.B1:MCBYV, KICK:=  acbyv4.l1b1, polarity=+1,mech_sep=-0.194;
MCBYV.B4L5.B1:MCBYV, KICK:=  acbyv4.l5b1, polarity=+1,mech_sep=-0.194;

MCBYH.4R1.B2:MCBYH, KICK:= -acbyhs4.r1b2, polarity=+1, mech_sep=-0.194*bv_aux;
MCBYH.4R5.B2:MCBYH, KICK:= -acbyhs4.r5b2, polarity=+1, mech_sep=-0.194*bv_aux;
MCBYH.A4L1.B2:MCBYH, KICK:= -acbyhs4.l1b2, polarity=+1, mech_sep= 0.194*bv_aux;
MCBYH.A4L5.B2:MCBYH, KICK:= -acbyhs4.l5b2, polarity=+1, mech_sep= 0.194*bv_aux;
MCBYH.B4L1.B2:MCBYH, KICK:= -acbyh4.l1b2, polarity=+1, mech_sep= 0.194*bv_aux;
MCBYH.B4L5.B2:MCBYH, KICK:= -acbyh4.l5b2, polarity=+1, mech_sep= 0.194*bv_aux;

MCBYV.4L1.B2:MCBYV, KICK:= -acbyvs4.l1b2*bv_aux, polarity=+1,, mech_sep= 0.194*bv_aux;
MCBYV.4L5.B2:MCBYV, KICK:= -acbyvs4.l5b2*bv_aux, polarity=+1, mech_sep= 0.194*bv_aux;
MCBYV.A4R1.B2:MCBYV, KICK:= -acbyvs4.r1b2*bv_aux, polarity=+1, mech_sep=-0.194*bv_aux;
MCBYV.A4R5.B2:MCBYV, KICK:= -acbyvs4.r5b2*bv_aux, polarity=+1, mech_sep=-0.194*bv_aux;
MCBYV.B4R1.B2:MCBYV, KICK:= -acbyv4.r1b2*bv_aux, polarity=+1, mech_sep=-0.194*bv_aux;
MCBYV.B4R5.B2:MCBYV, KICK:= -acbyv4.r5b2*bv_aux, polarity=+1, mech_sep=-0.194*bv_aux;

arcSEP=0.194;
exec,newel_ir15_b12(BPMYA.4,BPMYA,arcSEP);

exec,install_ir15_b12(BPMYA.4, posBPMYA.4);
exec,install_ir15_b12(MQY.4 , posQ4      );

! alternative orientation
if(bv_aux == 1){
seqedit,sequence=lhcb1;
! .... MCBY.B4
install,element=MCBYV.B4L1.B1, at=-(posQ4-dposMCBY.B4), from= IP1.L1;!IP side
install,element=MCBYH.B4R1.B1, at= (posQ4-dposMCBY.B4), from= IP1;   !IP side
install,element=MCBYV.B4L5.B1, at=-(posQ4-dposMCBY.B4), from= IP5;   !IP side
install,element=MCBYH.B4R5.B1, at= (posQ4-dposMCBY.B4), from= IP5;   !IP side
!
! .... MCBY.4
install,element=MCBYH.4L1.B1, at=-(posQ4-dposMCBY.4), from= IP1.L1;!IP side
install,element=MCBYV.4R1.B1, at= (posQ4-dposMCBY.4), from= IP1;   !IP side
install,element=MCBYH.4L5.B1, at=-(posQ4-dposMCBY.4), from= IP5;   !IP side
install,element=MCBYV.4R5.B1, at= (posQ4-dposMCBY.4), from= IP5;   !IP side
!
! .... MCBY.A4
install,element=MCBYV.A4L1.B1, at=-(posQ4-dposMCBY.A4), from= IP1.L1;!IP side
install,element=MCBYH.A4R1.B1, at= (posQ4-dposMCBY.A4), from= IP1;   !IP side
install,element=MCBYV.A4L5.B1, at=-(posQ4-dposMCBY.A4), from= IP5;   !IP side
install,element=MCBYH.A4R5.B1, at= (posQ4-dposMCBY.A4), from= IP5;   !IP side
endedit;
};

seqedit,sequence=lhcb2;
! .... MCBY.B4
install,element=MCBYH.B4L1.B2, at=-(posQ4-dposMCBY.B4)*bv_aux, from= IP1.L1;!IP side
install,element=MCBYV.B4R1.B2, at= (posQ4-dposMCBY.B4)*bv_aux, from= IP1;   !IP side
install,element=MCBYH.B4L5.B2, at=-(posQ4-dposMCBY.B4)*bv_aux, from= IP5;   !IP side
install,element=MCBYV.B4R5.B2, at= (posQ4-dposMCBY.B4)*bv_aux, from= IP5;   !IP side

! .... MCBY.4
install,element=MCBYV.4L1.B2, at=-(posQ4-dposMCBY.4)*bv_aux, from= IP1.L1;!IP side
install,element=MCBYH.4R1.B2, at= (posQ4-dposMCBY.4)*bv_aux, from= IP1;   !IP side
install,element=MCBYV.4L5.B2, at=-(posQ4-dposMCBY.4)*bv_aux, from= IP5;   !IP side
install,element=MCBYH.4R5.B2, at= (posQ4-dposMCBY.4)*bv_aux, from= IP5;   !IP side
!
! .... MCBY.A4
install,element=MCBYH.A4L1.B2, at=-(posQ4-dposMCBY.A4)*bv_aux, from= IP1.L1;!IP side
install,element=MCBYV.A4R1.B2, at= (posQ4-dposMCBY.A4)*bv_aux, from= IP1;   !IP side
install,element=MCBYH.A4L5.B2, at=-(posQ4-dposMCBY.A4)*bv_aux, from= IP5;   !IP side
install,element=MCBYV.A4R5.B2, at= (posQ4-dposMCBY.A4)*bv_aux, from= IP5;   !IP side
endedit;


!===============================================
! Q5
!===============================================
!if (l.MQYL==0){l.MQYL=3.40;};

!MQYL:  QUADRUPOLE, L := l.MQYL   ; ! long MQY type

l.BPMQB= 0.0;! l.BPMQB = 0
BPMQB: MONITOR,    L := l.BPMQB  ; ! large ap. for Q5 maybe from old q4 ones

MQY.5L1.B1:MQY,K1 :=  kq5.l1b1,        polarity=-1, mech_sep=-0.194       ;
MQY.5L5.B1:MQY,K1 :=  kq5.l5b1,        polarity=-1, mech_sep=-0.194       ;
MQY.5R1.B1:MQY,K1 :=  kq5.r1b1,        polarity=+1, mech_sep= 0.194       ;
MQY.5R5.B1:MQY,K1 :=  kq5.r5b1,        polarity=+1, mech_sep= 0.194       ;
MQY.5L1.B2:MQY,K1 := -kq5.l1b2*bv_aux, polarity=+1, mech_sep= 0.194*bv_aux;
MQY.5L5.B2:MQY,K1 := -kq5.l5b2*bv_aux, polarity=+1, mech_sep= 0.194*bv_aux;
MQY.5R1.B2:MQY,K1 := -kq5.r1b2*bv_aux, polarity=-1, mech_sep=-0.194*bv_aux;
MQY.5R5.B2:MQY,K1 := -kq5.r5b2*bv_aux, polarity=-1, mech_sep=-0.194*bv_aux;

!MCBYH.5R1.B1: MCBYH, KICK :=  acbch5.r1b1, polarity=+1;
!MCBYV.5L5.B1: MCBYV, KICK :=  acbcv5.l5b1*bv_aux, polarity=+1;
!MCBYH.5R5.B1: MCBYH, KICK :=  acbch5.r5b1, polarity=+1;
!MCBYV.5L1.B1: MCBYV, KICK :=  acbcv5.l1b1*bv_aux, polarity=+1;
!MCBYV.5R1.B2: MCBYV, KICK := -acbcv5.r1b2*bv_aux, polarity=+1;
!MCBYH.5L5.B2: MCBYH, KICK := -acbch5.l5b2, polarity=+1;
!MCBYV.5R5.B2: MCBYV, KICK := -acbcv5.r5b2*bv_aux, polarity=+1;
!MCBYH.5L1.B2: MCBYH, KICK := -acbch5.l1b2, polarity=+1;

! MQY B - A for all MQY the B corrector is closest to the MQY
!MCBYV.A5R1.B2:MCBYV, KICK:= acbyvs5.r1b2*bv_aux, polarity=+1;
!MCBYH.5R1.B2 :MCBYH, KICK:= acbyhs5.r1b2, polarity=+1;
!MCBYV.B5R1.B2:MCBYV, KICK:= acbyv5.r1b2*bv_aux, polarity=+1;
!MCBYH.B5L5.B2:MCBYH, KICK:= acbyh5.l5b2, polarity=+1;
!MCBYV.5L5.B2 :MCBYV, KICK:= acbyvs5.l5b2*bv_aux, polarity=+1;
!MCBYH.A5L5.B2:MCBYH, KICK:= acbyhs5.l5b2, polarity=+1;
!MCBYV.A5R5.B2:MCBYV, KICK:= acbyvs5.r5b2*bv_aux, polarity=+1;
!MCBYH.5R5.B2 :MCBYH, KICK:= acbyhs5.r5b2, polarity=+1;
!MCBYV.B5R5.B2:MCBYV, KICK:= acbyv5.r5b2*bv_aux, polarity=+1;
!MCBYH.B5L1.B2:MCBYH, KICK:= acbyh5.l1b2, polarity=+1;
!MCBYV.5L1.B2 :MCBYV, KICK:= acbyvs5.l1b2*bv_aux, polarity=+1;
!MCBYH.A5L1.B2:MCBYH, KICK:= acbyhs5.l1b2, polarity=+1;
!
!MCBYH.A5R1.B1:MCBYH, KICK:= -acbyhs5.r1b1, polarity=+1;
!MCBYV.5R1.B1 :MCBYV, KICK:= -acbyvs5.r1b1*bv_aux, polarity=+1;
!MCBYH.B5R1.B1:MCBYH, KICK:= -acbyh5.r1b1, polarity=+1;
!MCBYV.B5L5.B1:MCBYV, KICK:= -acbyv5.l5b1*bv_aux, polarity=+1;
!MCBYH.5L5.B1 :MCBYH, KICK:= -acbyhs5.l5b1, polarity=+1;
!MCBYV.A5L5.B1:MCBYV, KICK:= -acbyvs5.l5b1*bv_aux, polarity=+1;
!MCBYH.A5R5.B1:MCBYH, KICK:= -acbyhs5.r5b1, polarity=+1;
!MCBYV.5R5.B1 :MCBYV, KICK:= -acbyvs5.r5b1*bv_aux, polarity=+1;
!MCBYH.B5R5.B1:MCBYH, KICK:= -acbyh5.r5b1, polarity=+1;
!MCBYV.B5L1.B1:MCBYV, KICK:= -acbyv5.l1b1*bv_aux, polarity=+1;
!MCBYH.5L1.B1 :MCBYH, KICK:= -acbyhs5.l1b1, polarity=+1;
!MCBYV.A5L1.B1:MCBYV, KICK:= -acbyvs5.l1b1*bv_aux, polarity=+1;


posQ5  := 196.490 + deltaPosQ5; !new layout (excel file): 207.49

!position set for IR5 right
!dposBPMY.5  := l.MQY/2 + 0.974; !BPMYA.4L5.B1:BPMYA BPMYA.4L5.B2:BPMYA
!dposMCBY.B5 := (l.MQY + l.MCBYV)/2 + 0.372; !same as nominal note that in IR2-8-6 distance can
!dposMCBY.5  := dposMCBY.B5 + (l.MCBYH + l.MCBYV)/2 + 0.396; !be better
!dposMCBY.A5 := dposMCBY.5  + (l.MCBYV + l.MCBYH)/2 + 0.396;
!
!BPMYA.5R1.B1:BPMYA;
!BPMYA.5L5.B1:BPMYA;
!BPMYA.5R5.B1:BPMYA;
!BPMYA.5L1.B1:BPMYA;
!BPMYA.5R1.B2:BPMYA;
!BPMYA.5L5.B2:BPMYA;
!BPMYA.5R5.B2:BPMYA;
!BPMYA.5L1.B2:BPMYA;


!exec,newel_ir15_b12(BPMQB.5,BPMQB);
!exec,newel_ir15_b12(BPMYA.5,BPMYA);

exec,install_ir15_b12(MQML.5,   posQ5 );

dposMCBC := (l.MQML + l.MCBCH)/2 + 0.192;
dposBPM   := l.MQML/2 + 0.745; !3.145

value,posQ5-dposBPM,posQ5+dposBPM;
value,posQ5+dposMCBC,posQ5-dposMCBC;

!value,posQ5,dposBPMY.5,dposMCBY.B5,dposMCBY.5,dposMCBY.A5;

if(bv_aux == 1){
seqedit,sequence=lhcb1;
! .... BPMYA.5
install,element=BPMR.5L1.B1, at=-(posQ5-dposBPM), from= IP1.L1;!IP side
install,element=BPM.5R1.B1,  at= (posQ5+dposBPM), from= IP1;   !non-IP side
install,element=BPMR.5L5.B1, at=-(posQ5+dposBPM), from= IP5;   !non-IP side
install,element=BPM.5R5.B1,  at= (posQ5-dposBPM), from= IP5;   !IP side
!
! .... MCBC.5
install,element=MCBCV.5L1.B1, at=-(posQ5+dposMCBC), from= IP1.L1;!non-IP side
install,element=MCBCH.5R1.B1, at= (posQ5-dposMCBC), from= IP1;   !IP side
install,element=MCBCV.5L5.B1, at=-(posQ5-dposMCBC), from= IP5;   !IP side
install,element=MCBCH.5R5.B1, at= (posQ5+dposMCBC), from= IP5;   !non-IP side

endedit;
};

seqedit,sequence=lhcb2;
! .... BPMYA.5
install,element=BPM.5L1.B2, at=-(posQ5-dposBPM)*bv_aux, from= IP1.L1;!IP side
install,element=BPMR.5R1.B2,at= (posQ5+dposBPM)*bv_aux, from= IP1;   !non-IP side
install,element=BPM.5L5.B2, at=-(posQ5+dposBPM)*bv_aux, from= IP5;   !non-IP side
install,element=BPMR.5R5.B2,at= (posQ5-dposBPM)*bv_aux, from= IP5;   !IP side
!
! .... MCBY.B5
install,element=MCBCH.5L1.B2, at=-(posQ5+dposMCBC)*bv_aux, from= IP1.L1;!non-IP side
install,element=MCBCV.5R1.B2, at= (posQ5-dposMCBC)*bv_aux, from= IP1;   !IP side
install,element=MCBCH.5L5.B2, at=-(posQ5-dposMCBC)*bv_aux, from= IP5;   !IP side
install,element=MCBCV.5R5.B2, at= (posQ5+dposMCBC)*bv_aux, from= IP5;   !non-IP side
!
endedit;


! ===============================================
! Q6
! ===============================================
l.BPMQC= 0.0;!l.BPMQC= 0.100
BPMQC: MONITOR,    L := l.BPMQC  ; ! same as BPMR.6R[15] and BPM.6L[15]

posQ6     := 228.390 + deltaPosQ6;
dposMCBC := (l.MQML + l.MCBCH)/2 + 0.192;
dposBPM   := l.MQML/2 + 0.745; !3.145

value, posQ6,dposMCBC,dposBPM;

!exec,newel_ir15_b12(BPMQC.6,BPMQC);

exec,install_ir15_b12(MQML.6,    posQ6 );


! no change with respect to the nominal LHC
! Nominal: MCBC Q6 BPM IP1 MCBC Q6 BPM ; BPM Q6 MCBC IP5 BPM Q6 MCBC
! old Upgrade: IR1 Right: BPM Q6 MCBC ; IR5 Right MCBC Q6 BPM
if(bv_aux == 1){
seqedit,sequence=lhcb1;
! .... BPM.6
install,element=BPM.6L1.B1, at=-posQ6+dposBPM, from= IP1.L1;
install,element=BPMR.6R1.B1,at= posQ6+dposBPM, from= IP1;
install,element=BPM.6L5.B1, at=-posQ6-dposBPM, from= IP5;
install,element=BPMR.6R5.B1,at= posQ6-dposBPM, from= IP5;

! .... MCBC.6
install,element=MCBCH.6L1.B1, at=-posQ6-dposMCBC, from= IP1.L1;
install,element=MCBCV.6R1.B1, at= posQ6-dposMCBC, from= IP1;
install,element=MCBCH.6L5.B1, at=-posQ6+dposMCBC, from= IP5;
install,element=MCBCV.6R5.B1, at= posQ6+dposMCBC, from= IP5;
endedit;
};

seqedit,sequence=lhcb2;
! .... BPM.6
install,element=BPMR.6L1.B2,at=(-posQ6+dposBPM)*bv_aux, from= IP1.L1;
install,element=BPM.6R1.B2, at=( posQ6+dposBPM)*bv_aux, from= IP1;
install,element=BPMR.6L5.B2,at=(-posQ6-dposBPM)*bv_aux, from= IP5;
install,element=BPM.6R5.B2, at=( posQ6-dposBPM)*bv_aux, from= IP5;

! .... MCBC.6
install,element=MCBCV.6L1.B2, at=(-posQ6-dposMCBC)*bv_aux, from= IP1.L1;
install,element=MCBCH.6R1.B2, at=( posQ6-dposMCBC)*bv_aux, from= IP1;
install,element=MCBCV.6L5.B2, at=(-posQ6+dposMCBC)*bv_aux, from= IP5;
install,element=MCBCH.6R5.B2, at=( posQ6+dposMCBC)*bv_aux, from= IP5;
endedit;



! ===============================================
! MS in Q10
! ===============================================
posMSQ10    := l.MQML/2 +0.1795+l.MS/2;
posMCBQ10   := posMSQ10+l.MS/2 +0.085+l.MCBV/2;

MCBH.10L1.B1: MCBH, KICK := acbh10.l1b1,        polarity=+1, mech_sep=-0.194       ;
MCBH.10L5.B1: MCBH, KICK := acbh10.l5b1,        polarity=+1, mech_sep=-0.194       ;
MCBV.10R1.B1: MCBV, KICK := acbv10.r1b1*bv_aux, polarity=+1, mech_sep= 0.194       ;
MCBV.10R5.B1: MCBV, KICK := acbv10.r5b1*bv_aux, polarity=+1, mech_sep= 0.194       ;
MCBV.10L1.B2: MCBV, KICK := acbv10.l1b2*bv_aux, polarity=+1, mech_sep= 0.194*bv_aux;
MCBV.10L5.B2: MCBV, KICK := acbv10.l5b2*bv_aux, polarity=+1, mech_sep= 0.194*bv_aux;
MCBH.10R1.B2: MCBH, KICK := acbh10.r1b2,        polarity=+1, mech_sep=-0.194*bv_aux;
MCBH.10R5.B2: MCBH, KICK := acbh10.r5b2,        polarity=+1, mech_sep=-0.194*bv_aux;


DISABLE_SEXT(MS.10):macro={
MS.10L1.B1: MS,K2:=0;
MS.10R1.B1: MS,K2:=0;
MS.10L5.B1: MS,K2:=0;
MS.10R5.B1: MS,K2:=0;
MS.10L1.B2: MS,K2:=0, polarity=-1*bv_aux;
MS.10R1.B2: MS,K2:=0, polarity=+1*bv_aux;
MS.10L5.B2: MS,K2:=0, polarity=-1*bv_aux;
MS.10R5.B2: MS,K2:=0, polarity=+1*bv_aux;
on_cutMS.10=1;
};

ENABLE_SEXT(MS.10,ksf1,ksd2,ksd1,ksf2):macro={
MS.10L1.B1: MS,K2:=ksf1.a81b1;
MS.10R1.B1: MS,K2:=ksd2.a12b1;
MS.10L5.B1: MS,K2:=ksf1.a45b1;
MS.10R5.B1: MS,K2:=ksd2.a56b1;
MS.10L1.B2: MS,K2:=- ksd1.a81b2, polarity=-1*bv_aux;
MS.10R1.B2: MS,K2:=- ksf2.a12b2, polarity=+1*bv_aux;
MS.10L5.B2: MS,K2:=- ksd1.a45b2, polarity=-1*bv_aux;
MS.10R5.B2: MS,K2:=- ksf2.a56b2, polarity=+1*bv_aux;
on_cutMS.10=0;
};

DISABLE_SEXT_F(MS.10):macro={
MS.10L1.B1: MS,K2:=0;
MS.10L5.B1: MS,K2:=0;
MS.10R1.B2: MS,K2:=0, polarity=+1*bv_aux;
MS.10R5.B2: MS,K2:=0, polarity=+1*bv_aux;
on_cutMS.10f=1;
};

DISABLE_SEXT_F_ALT(MS.14):macro={
MS.14R8.B1: MS,K2:=0;
MS.14R4.B1: MS,K2:=0;
MS.14L2.B2: MS,K2:=0, polarity=+1*bv_aux;
MS.14L6.B2: MS,K2:=0, polarity=+1*bv_aux;
on_cutMS.14f=1;
};



DISABLE_SEXT_D(MS.10):macro={
MS.10R1.B1: MS,K2:=0;
MS.10R5.B1: MS,K2:=0;
MS.10L1.B2: MS,K2:=0, polarity=+1*bv_aux;
MS.10L5.B2: MS,K2:=0, polarity=+1*bv_aux;
on_cutMS.10d=1;
};

exec,ENABLE_SEXT(MS.10);

! Q10
if(bv_aux == 1){
seqedit,sequence=lhcb1;
install,element=MS.10L1.B1,  at=+posMSQ10, from=MQML.10L1.B1;
install,element=MS.10R1.B1,  at=+posMSQ10, from=MQML.10R1.B1;
install,element=MS.10L5.B1,  at=+posMSQ10, from=MQML.10L5.B1;
install,element=MS.10R5.B1,  at=+posMSQ10, from=MQML.10R5.B1;
install,element=MCBH.10L1.B1,at=posMCBQ10, from=MQML.10L1.B1;
install,element=MCBV.10R1.B1,at=posMCBQ10, from=MQML.10R1.B1;
install,element=MCBH.10L5.B1,at=posMCBQ10, from=MQML.10L5.B1;
install,element=MCBV.10R5.B1,at=posMCBQ10, from=MQML.10R5.B1;
endedit;
};

seqedit,sequence=lhcb2;
install,element=MS.10L1.B2,  at=+posMSQ10*bv_aux, from=MQML.10L1.B2;
install,element=MS.10R1.B2,  at=+posMSQ10*bv_aux, from=MQML.10R1.B2;
install,element=MS.10L5.B2,  at=+posMSQ10*bv_aux, from=MQML.10L5.B2;
install,element=MS.10R5.B2,  at=+posMSQ10*bv_aux, from=MQML.10R5.B2;
install,element=MCBV.10L1.B2,at=posMCBQ10*bv_aux, from=MQML.10L1.B2;
install,element=MCBH.10R1.B2,at=posMCBQ10*bv_aux, from=MQML.10R1.B2;
install,element=MCBV.10L5.B2,at=posMCBQ10*bv_aux, from=MQML.10L5.B2;
install,element=MCBH.10R5.B2,at=posMCBQ10*bv_aux, from=MQML.10R5.B2;
endedit;

value,posMSQ10,posMCBQ10;
value,on_cutMS.10,on_cutMS.14;
if (on_cutMS.10==1){exec, DISABLE_SEXT(MS.10);};
if (on_cutMS.14==1){exec, DISABLE_SEXT(MS.14);};
if (on_cutMS.10f==1){exec, DISABLE_SEXT_F(MS.10);};
if (on_cutMS.14f==1){exec, DISABLE_SEXT_F(MS.14);};
if (on_cutMS.10d==1){exec, DISABLE_SEXT_D(MS.10);};
if (on_cutMS.14d==1){exec, DISABLE_SEXT_D(MS.14);};

! ===============================================
! Crab cavities
! ===============================================


crabrf:=hrf400/lhclength*clight;

crab_make_tkicker(NNN,FFF): macro {
omega_crabsc:=twopi*HRF400/LHCLENGTH;
z_crab:=0; ! positive means head of the bunch i.e. z=-cT and z==t(madx)

phi_crab_l1b1 := 0;
phi_crab_r1b1 := 0;
phi_crab_l1b2 := 0;
phi_crab_r1b2 := 0;
phi_crab_l5b1 := 0;
phi_crab_r5b1 := 0;
phi_crab_l5b2 := 0;
phi_crab_r5b2 := 0;

sin_crab_l1b1:=sin(-omega_crabsc*z_crab+twopi*phi_crab_l1b1);
sin_crab_r1b1:=sin(-omega_crabsc*z_crab+twopi*phi_crab_r1b1);
sin_crab_l1b2:=sin(-omega_crabsc*z_crab+twopi*phi_crab_l1b2);
sin_crab_r1b2:=sin(-omega_crabsc*z_crab+twopi*phi_crab_r1b2);
sin_crab_l5b1:=sin(-omega_crabsc*z_crab+twopi*phi_crab_l5b1);
sin_crab_r5b1:=sin(-omega_crabsc*z_crab+twopi*phi_crab_r5b1);
sin_crab_l5b2:=sin(-omega_crabsc*z_crab+twopi*phi_crab_l5b2);
sin_crab_r5b2:=sin(-omega_crabsc*z_crab+twopi*phi_crab_r5b2);

!sin_crab:=sin(omega_crabsc*z_crab);
cosi1:=cos(twopi*phi_IR1/360.);sinu1:=sin(twopi*phi_IR1/360.);
cosi5:=cos(twopi*phi_IR5/360.);sinu5:=sin(twopi*phi_IR5/360.);
on_crab1=0;on_crab5=0;
acfga.4NNNl1.b1: tkicker, l=0, HKICK:=on_crab1*ahcrab_l1b1/FFF*sin_crab_l1b1*cosi1,VKICK:=on_crab1*avcrab_l1b1/FFF*sin_crab_l1b1*sinu1*bv_aux, mech_sep=-0.194;
acfga.4NNNl1.b2: tkicker, l=0, HKICK:=on_crab1*ahcrab_l1b2/FFF*sin_crab_l1b2*cosi1,VKICK:=on_crab1*avcrab_l1b2/FFF*sin_crab_l1b2*sinu1*bv_aux, mech_sep= 0.194*bv_aux;
acfga.4NNNr1.b1: tkicker, l=0, HKICK:=on_crab1*ahcrab_r1b1/FFF*sin_crab_r1b1*cosi1,VKICK:=on_crab1*avcrab_r1b1/FFF*sin_crab_r1b1*sinu1*bv_aux, mech_sep= 0.194;
acfga.4NNNr1.b2: tkicker, l=0, HKICK:=on_crab1*ahcrab_r1b2/FFF*sin_crab_r1b2*cosi1,VKICK:=on_crab1*avcrab_r1b2/FFF*sin_crab_r1b2*sinu1*bv_aux, mech_sep=-0.194*bv_aux;
acfga.4NNNl5.b1: tkicker, l=0, HKICK:=on_crab5*ahcrab_l5b1/FFF*sin_crab_l5b1*cosi5,VKICK:=on_crab5*avcrab_l5b1/FFF*sin_crab_l5b1*sinu5*bv_aux, mech_sep=-0.194;
acfga.4NNNl5.b2: tkicker, l=0, HKICK:=on_crab5*ahcrab_l5b2/FFF*sin_crab_l5b2*cosi5,VKICK:=on_crab5*avcrab_l5b2/FFF*sin_crab_l5b2*sinu5*bv_aux, mech_sep= 0.194*bv_aux;
acfga.4NNNr5.b1: tkicker, l=0, HKICK:=on_crab5*ahcrab_r5b1/FFF*sin_crab_r5b1*cosi5,VKICK:=on_crab5*avcrab_r5b1/FFF*sin_crab_r5b1*sinu5*bv_aux, mech_sep= 0.194;
acfga.4NNNr5.b2: tkicker, l=0, HKICK:=on_crab5*ahcrab_r5b2/FFF*sin_crab_r5b2*cosi5,VKICK:=on_crab5*avcrab_r5b2/FFF*sin_crab_r5b2*sinu5*bv_aux, mech_sep=-0.194*bv_aux;
};

crab_make_marker(NNN): macro {
acfga.4NNNl1.b1: marker, mech_sep=-0.194;
acfga.4NNNl1.b2: marker, mech_sep= 0.194*bv_aux;
acfga.4NNNr1.b1: marker, mech_sep= 0.194;
acfga.4NNNr1.b2: marker, mech_sep=-0.194*bv_aux;
acfga.4NNNl5.b1: marker, mech_sep=-0.194;
acfga.4NNNl5.b2: marker, mech_sep= 0.194*bv_aux;
acfga.4NNNr5.b1: marker, mech_sep= 0.194;
acfga.4NNNr5.b2: marker, mech_sep=-0.194*bv_aux;
};

crab_make_vac_marker(NNN): macro={
vacfga.4NNNl1.b1: marker, mech_sep=-0.194;
vacfga.4NNNl1.b2: marker, mech_sep= 0.194*bv_aux;
vacfga.4NNNr1.b1: marker, mech_sep= 0.194;
vacfga.4NNNr1.b2: marker, mech_sep=-0.194*bv_aux;
vacfga.4NNNl5.b1: marker, mech_sep=-0.194;
vacfga.4NNNl5.b2: marker, mech_sep= 0.194*bv_aux;
vacfga.4NNNr5.b1: marker, mech_sep= 0.194;
vacfga.4NNNr5.b2: marker, mech_sep=-0.194*bv_aux;
};


_crab_make_rf(NNN,SIDE,IRN,BIM,FFF): macro {
    acfga.4NNNSIDEIRN.BIM: rfmultipole,l=0, freq=crabrf,
    knl:={0,kn2_crabNNNSIDEIRNBIM,
          kn3_crabNNNSIDEIRNBIM,kn4_crabNNNSIDEIRNBIM},
    ksl:={0,ks2_crabNNNSIDEIRNBIM,
          ks3_crabNNNSIDEIRNBIM,ks4_crabNNNSIDEIRNBIM},
    pnl:={0,pn2_crabNNNSIDEIRNBIM,pn3_crabNNNSIDEIRNBIM,pn4_crabNNNSIDEIRNBIM},
    psl:={0,ps2_crabNNNSIDEIRNBIM,ps3_crabNNNSIDEIRNBIM,ps4_crabNNNSIDEIRNBIM};
};

_crab_make_cc(nnn,side,irn,bim,fff): macro {
    !sigmas=0.075;
    vhcrabref_sideirnbim:=ahcrab_sideirnbim/fff*cosiirn*nrj*1e9/1e6;
    vvcrabref_sideirnbim:=avcrab_sideirnbim/fff*sinuirn*nrj*1e9/1e6;
    acfgah.4nnnsideirn.bim: crabcavity, l=0,
      volt:=on_crabirn*vhcrabref_sideirnbim,
      lag:=phi_crab_sideirnbim,
      freq=crabrf/1e6,tilt=0,rv3=1e9,rv4=1e9;
    acfgav.4nnnsideirn.bim: crabcavity, l=0,
      volt:=on_crabirn*vvcrabref_sideirnbim,
      lag:=phi_crab_sideirnbim,
      freq=crabrf/1e6,tilt=pi/2,rv3=1e9,rv4=1e9;
    print,text="making acfgah.4nnnsideirn.bim";
    print,text="making acfgav.4nnnsideirn.bim";
};

crab_make_cc(NNN,FFF): macro {
exec,_crab_make_cc(NNN,l,5,b1,FFF);
exec,_crab_make_cc(NNN,l,5,b2,FFF);
exec,_crab_make_cc(NNN,r,5,b1,FFF);
exec,_crab_make_cc(NNN,r,5,b2,FFF);
exec,_crab_make_cc(NNN,l,1,b1,FFF);
exec,_crab_make_cc(NNN,l,1,b2,FFF);
exec,_crab_make_cc(NNN,r,1,b1,FFF);
exec,_crab_make_cc(NNN,r,1,b2,FFF);
};

crab_make_rf(NNN,FFF): macro {
exec,_crab_make_rf(NNN,l,5,b1,FFF);
exec,_crab_make_rf(NNN,l,5,b2,FFF);
exec,_crab_make_rf(NNN,r,5,b1,FFF);
exec,_crab_make_rf(NNN,r,5,b2,FFF);
exec,_crab_make_rf(NNN,l,1,b1,FFF);
exec,_crab_make_rf(NNN,l,1,b2,FFF);
exec,_crab_make_rf(NNN,r,1,b1,FFF);
exec,_crab_make_rf(NNN,r,1,b2,FFF);
};

!PF1 Position IP side (front) Point 1
!PB1 Position nonIP side (back) Point 1

crab_install(NNN,P1B1,P1B2,P5B1,P5B2): macro ={
value,P1B1,P1B2,P5B1,P5B2;
if(bv_aux == 1){
seqedit, sequence=lhcb1;
install,element=acfga.4NNNl5.b1,at=-P5B1,from=IP5;
install,element=acfga.4NNNr5.b1,at= P5B1,from=IP5;
install,element=acfga.4NNNl1.b1,at=-P1B1,from=IP1.L1;
install,element=acfga.4NNNr1.b1,at= P1B1,from=IP1;
install,element=vacfga.4NNNl5.b1,at=-P5B2,from=IP5;
install,element=vacfga.4NNNr5.b1,at= P5B2,from=IP5;
install,element=vacfga.4NNNl1.b1,at=-P1B2,from=IP1.L1;
install,element=vacfga.4NNNr1.b1,at= P1B2,from=IP1;
endedit;
};
seqedit, sequence=lhcb2;
install,element=acfga.4NNNl5.b2,at=-P5B2*bv_aux,from=IP5;
install,element=acfga.4NNNr5.b2,at= P5B2*bv_aux,from=IP5;
install,element=acfga.4NNNl1.b2,at=-P1B2*bv_aux,from=IP1.L1;
install,element=acfga.4NNNr1.b2,at= P1B2*bv_aux,from=IP1;
install,element=vacfga.4NNNl5.b2,at=-P5B1*bv_aux,from=IP5;
install,element=vacfga.4NNNr5.b2,at= P5B1*bv_aux,from=IP5;
install,element=vacfga.4NNNl1.b2,at=-P1B1*bv_aux,from=IP1.L1;
install,element=vacfga.4NNNr1.b2,at= P1B1*bv_aux,from=IP1;
endedit;
};


crab_install_cc(NNN,P1B1,P1B2,P5B1,P5B2): macro ={
if(bv_aux == 1){
seqedit, sequence=lhcb1;
install,element=acfgah.4NNNl5.b1,at=-P5B1,from=IP5;
install,element=acfgah.4NNNr5.b1,at= P5B1,from=IP5;
install,element=acfgah.4NNNl1.b1,at=-P1B1,from=IP1.L1;
install,element=acfgah.4NNNr1.b1,at= P1B1,from=IP1;
install,element=acfgav.4NNNl5.b1,at=-P5B1,from=IP5;
install,element=acfgav.4NNNr5.b1,at= P5B1,from=IP5;
install,element=acfgav.4NNNl1.b1,at=-P1B1,from=IP1.L1;
install,element=acfgav.4NNNr1.b1,at= P1B1,from=IP1;
endedit;
};
seqedit, sequence=lhcb2;
install,element=acfgah.4NNNl5.b2,at=-P5B2*bv_aux,from=IP5;
install,element=acfgah.4NNNr5.b2,at= P5B2*bv_aux,from=IP5;
install,element=acfgah.4NNNl1.b2,at=-P1B2*bv_aux,from=IP1.L1;
install,element=acfgah.4NNNr1.b2,at= P1B2*bv_aux,from=IP1;
install,element=acfgav.4NNNl5.b2,at=-P5B2*bv_aux,from=IP5;
install,element=acfgav.4NNNr5.b2,at= P5B2*bv_aux,from=IP5;
install,element=acfgav.4NNNl1.b2,at=-P1B2*bv_aux,from=IP1.L1;
install,element=acfgav.4NNNr1.b2,at= P1B2*bv_aux,from=IP1;
endedit;
};


crab_remove(NNN): macro ={
if(bv_aux == 1){
seqedit, sequence=lhcb1;
remove,element=acfga.4NNNl5.b1;
remove,element=acfga.4NNNr5.b1;
remove,element=acfga.4NNNl1.b1;
remove,element=acfga.4NNNr1.b1;
remove,element=acfgah.4NNNl5.b1;
remove,element=acfgah.4NNNr5.b1;
remove,element=acfgah.4NNNl1.b1;
remove,element=acfgah.4NNNr1.b1;
remove,element=acfgav.4NNNl5.b1;
remove,element=acfgav.4NNNr5.b1;
remove,element=acfgav.4NNNl1.b1;
remove,element=acfgav.4NNNr1.b1;
remove,element=vacfga.4NNNl5.b1;
remove,element=vacfga.4NNNr5.b1;
remove,element=vacfga.4NNNl1.b1;
remove,element=vacfga.4NNNr1.b1;
endedit;
};
seqedit, sequence=lhcb2;
remove,element=acfga.4NNNl5.b2;
remove,element=acfga.4NNNr5.b2;
remove,element=acfga.4NNNl1.b2;
remove,element=acfga.4NNNr1.b2;
remove,element=acfgah.4NNNl5.b2;
remove,element=acfgah.4NNNr5.b2;
remove,element=acfgah.4NNNl1.b2;
remove,element=acfgah.4NNNr1.b2;
remove,element=acfgav.4NNNl5.b2;
remove,element=acfgav.4NNNr5.b2;
remove,element=acfgav.4NNNl1.b2;
remove,element=acfgav.4NNNr1.b2;
remove,element=vacfga.4NNNl5.b2;
remove,element=vacfga.4NNNr5.b2;
remove,element=vacfga.4NNNl1.b2;
remove,element=vacfga.4NNNr1.b2;
endedit;
};

exec,crab_make_tkicker(A,2);
exec,crab_make_tkicker(B,2);

exec,crab_make_vac_marker(A);
exec,crab_make_vac_marker(B);


exec,crab_remove(A);
exec,crab_remove(B);


! layout
! B1 B2 IP B2 B1

posRFDB2A=155.885; posDQWB2A=155.975;
posRFDB2B=157.005; posDQWB2B=156.875;
posRFDB1A=159.921; posDQWB1A=159.549;
posRFDB1B=161.041; posDQWB1B=160.449;



!staggered          IR1B1     IR1B2     IR5B1     IR5B2
exec,crab_install(A,posRFDB1A,posRFDB2A,posDQWB1A,posDQWB2A);
exec,crab_install(B,posRFDB1B,posRFDB2B,posDQWB1B,posDQWB2B);

!===============================================
! Masks
!===============================================
!compared to HLLHCV1.1:
!- mask in front of D2 removed
!- mask in front of Q4 removed

l.TCLMB=1.0;
l.TCLMC=1.0;

!posTCLMC.5  := posQ5 - dposMCBY.A5 - l.MCBY/2 - l.TCLMB/2 - 1.835;!930+655+500/2
!posTCLMC.6C := posQ6 - dposMCBCH - l.TCLMC/2 - 1.831;!926+655+500/2
!posTCLMC.6Q := posQ6 - dposMCBCH - l.TCLMC/2 - 1.831;
!value,posTCLMC.5,posTCLMC.6C,posTCLMC.6Q;

! end q4   vac - tcl mech length     pos MCBY
! 173022 - 759 - 1500/2 =171513      174939
posTCLMB.4C := posQ4 - dposmcby.a4 - 3.461;! Integration meeting 18/1
posTCLMC.5C := posQ5 - l.mqml/2-0.192-l.mcbch-0.926-1.405;
posTCLMC.5Q := posQ5 - l.mqml/2-2.955;!- 5.216;
posTCLMC.6C := posQ6 - l.mqml/2-0.192-l.mcbch-0.926-1.405;
posTCLMC.6Q := posQ6 - l.mqml/2-2.816;!- 5.216;

TCLMB : COLLIMATOR, L := l.TCLMB;
TCLMC : COLLIMATOR, L := l.TCLMC;


exec,newel_ir15_b12(TCLMB.4,TCLMB,arcSEP);
exec,newel_ir15_b12(TCLMC.5,TCLMC,arcSEP);
exec,newel_ir15_b12(TCLMC.6,TCLMC,arcSEP);

!mask Q5
exec,install_ir15_b1_b2_inv(TCLMB.4, posTCLMB.4C, posTCLMB.4C );
exec,install_ir15_b1_b2_inv(TCLMC.5, posTCLMC.5C, posTCLMC.5Q );
exec,install_ir15_b1_b2_inv(TCLMC.6, posTCLMC.6C, posTCLMC.6Q );

!!mask Q6
!if(bv_aux == 1){
!seqedit,sequence=lhcb1;
!! ... TCLMC
!install,element=TCLMC.6L1.B1,at=-posTCLMC.6C, from= IP1.L1;
!install,element=TCLMC.6R1.B1,at= posTCLMC.6Q, from= IP1;
!install,element=TCLMC.6L5.B1,at=-posTCLMC.6Q, from= IP5;
!install,element=TCLMC.6R5.B1,at= posTCLMC.6C, from= IP5;
!endedit;
!};
!
!seqedit,sequence=lhcb2;
!! ... TCLMC
!install,element=TCLMC.6L1.B2, at=(-posTCLMC.6C)*bv_aux, from= IP1.L1;
!install,element=TCLMC.6R1.B2, at=( posTCLMC.6Q)*bv_aux, from= IP1;
!install,element=TCLMC.6L5.B2, at=(-posTCLMC.6Q)*bv_aux, from= IP5;
!install,element=TCLMC.6R5.B2, at=( posTCLMC.6C)*bv_aux, from= IP5;
!endedit;

!===============================================
! TAN, TCTs, TCLs
!===============================================


if (L.TAXN==0) {L.TAXN=3.492;};

if (L.TCLPX ==0) {L.TCLPX  =1;};
if (L.TCTPXH==0) {L.TCTPXH =1;};
if (L.TCTPXV==0) {L.TCTPXV =1;};


value,L.TAXN,L.TCLPX,L.TCTPXH,L.TCTPXV;
! shielding collimation
! v6.503  10.425 ,   7.335 ,   5.655 ,   3.445 ,   2.0805
! slhc 1.275 closer to D2
!D2 region, posBPMWQ = BPM right in front of D2

! Gentini Option 2 (starts at 137340)

posTCLPX   := posD2-l.MBRD/2-0.866-1.726 + deltaposTAXN;
posTCTPXH  := posTCLPX-1.586 ;
posTCTPXV  := posTCTPXH-1.5385 ;
posTAXN   := posTCTPXV-1.1625-0.183-l.TAXN/2;
!old layout TAXN non-ip side 128801+3332/2=130467
!           TAXN flange non-ip side 137640-(1480+523+1476+281+1476+208+285+702+480)=130729
!           TAXN flange 130729-130467 =  262

posTAXNA  := posTAXN - L.TAXN/2;
posTAXNB  := posTAXN + L.TAXN/2;
posTCLPXA  := posTCLPX - L.TCLPX/2;
posTCLPXB  := posTCLPX + L.TCLPX/2;
posTCTPXHA  := posTCTPXH - L.TCTPXH/2;
posTCTPXHB  := posTCTPXH + L.TCTPXH/2;
posTCTPXVA  := posTCTPXV - L.TCTPXV/2;
posTCTPXVB  := posTCTPXV + L.TCTPXV/2;

sepTCLPX   := round((posTCLPX -posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXH  := round((posTCTPXH-posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXV  := round((posTCTPXV-posD1)/(posD2-posD1)*194*10)/1e4;
sepTCLPXA  := round((posTCLPXA -posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXHA := round((posTCTPXHA-posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXVA := round((posTCTPXVA-posD1)/(posD2-posD1)*194*10)/1e4;
sepTCLPXB  := round((posTCLPXB -posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXHB := round((posTCTPXHB-posD1)/(posD2-posD1)*194*10)/1e4;
sepTCTPXVB := round((posTCTPXVB-posD1)/(posD2-posD1)*194*10)/1e4;

sepTAXN   := round((posTAXN -posD1)/(posD2-posD1)*194)/1e3;
sepTAXNA  := round((posTAXNA -posD1)/(posD2-posD1)*194)/1e3;
sepTAXNB  := round((posTAXNB -posD1)/(posD2-posD1)*194)/1e3;

value,posTAXN,posTAXNA,posTAXNB;
value,posTCLPX,posTCLPXA,posTCLPXB;
value,posTCTPXH,posTCTPXHA,posTCTPXHB;
value,posTCTPXV,posTCTPXVA,posTCTPXVB;

value,sepTAXN,sepTAXNA,sepTAXNB;
value,sepTCLPX,sepTCLPXA,sepTCLPXB;
value,sepTCTPXH,sepTCTPXHA,sepTCTPXHB;
value,sepTCTPXV,sepTCTPXVA,sepTCTPXVB;


!Q5/Q6 region
posTCL.5C := posQ5 - l.mqml/2-0.192-l.mcbch-0.926-1.544-2.010  ; !was 1.760
posTCL.5Q := posQ5 - l.mqml/2-1.411-1.544-2.010;! 6.976 ;
posTCL.6C := posQ6 - l.mqml/2-0.192-l.mcbch-0.926-1.405-2.010  ; !was 1.760
posTCL.6Q := posQ6 - l.mqml/2-1.411-1.405-2.010;! 6.976 ;

!posTCTPXV.6Q := posQ5 + l.mqml/2+1.435+1.915; !8.7655 - 4.235; !left !was 1.664
!posTCTPH.6Q := posQ5 + l.mqml/2+1.435+1.915+2.000;!10.765 - 4.235; !left

posTCTPXV.6Q := posQ5 + l.mqml/2+1.411+2.949;
posTCTPH.6Q := posQ5 + l.mqml/2 +1.411+2.949+2.000;
posTCTPXV.6C := posQ5 + l.mqml/2+2.018+2.949;
posTCTPH.6C := posQ5 + l.mqml/2 +2.018+2.949+2.000;


value,posTCL.5C,posTCL.5Q,posTCL.6C,posTCL.6Q;
value,posTCTPXV.6Q,posTCTPH.6Q,posTCTPXV.6C,posTCTPH.6C;


L.TCTPH=1;
L.TCTPV=1;

TCTPH: collimator, L:=L.TCTPH;
TCTPV: collimator, L:=L.TCTPV;

L.TCLPX=1;
TCLPX: collimator, L:=L.TCLPX;

TCLPX.4R1.B1: TCLPX, mech_sep= sepTCLPX;
TCLPX.4L1.B2: TCLPX, mech_sep= sepTCLPX*bv_aux;
TCLPX.4R5.B1: TCLPX, mech_sep= sepTCLPX;
TCLPX.4L5.B2: TCLPX, mech_sep= sepTCLPX*bv_aux;

VTCLPX.4R1.A.B2: marker, mech_sep=-sepTCLPX;
VTCLPX.4L1.A.B1: marker, mech_sep=-sepTCLPX*bv_aux;
VTCLPX.4R5.A.B2: marker, mech_sep=-sepTCLPX;
VTCLPX.4L5.A.B1: marker, mech_sep=-sepTCLPX*bv_aux;

VTCLPX.4R1.B.B2: marker, mech_sep=-sepTCLPX;
VTCLPX.4L1.B.B1: marker, mech_sep=-sepTCLPX*bv_aux;
VTCLPX.4R5.B.B2: marker, mech_sep=-sepTCLPX;
VTCLPX.4L5.B.B1: marker, mech_sep=-sepTCLPX*bv_aux;


TCL.5R1.B1: TCL, mech_sep= 0.194;
TCL.5L1.B2: TCL, mech_sep= 0.194*bv_aux;
TCL.5R5.B1: TCL, mech_sep= 0.194;
TCL.5L5.B2: TCL, mech_sep= 0.194*bv_aux;



TCL.6R1.B1: TCL, mech_sep= 0.194;
TCL.6L1.B2: TCL, mech_sep= 0.194*bv_aux;
TCL.6R5.B1: TCL, mech_sep= 0.194;
TCL.6L5.B2: TCL, mech_sep= 0.194*bv_aux;




TAXN: collimator, l:=l.TAXN;

TAXN.4R1: TAXN;
TAXN.4R5: TAXN;
TAXN.4L1: TAXN;
TAXN.4L5: TAXN;

VTAXNA.4L1.B1: marker, mech_sep=-sepTAXNA;
VTAXNA.4L1.B2: marker, mech_sep= sepTAXNA*bv_aux;
VTAXNA.4L5.B1: marker, mech_sep=-sepTAXNA;
VTAXNA.4L5.B2: marker, mech_sep= sepTAXNA*bv_aux;
VTAXNA.4R1.B1: marker, mech_sep= sepTAXNA;
VTAXNA.4R1.B2: marker, mech_sep=-sepTAXNA*bv_aux;
VTAXNA.4R5.B1: marker, mech_sep= sepTAXNA;
VTAXNA.4R5.B2: marker, mech_sep=-sepTAXNA*bv_aux;
VTAXNB.4L1.B1: marker, mech_sep=-sepTAXNB;
VTAXNB.4L1.B2: marker, mech_sep= sepTAXNB*bv_aux;
VTAXNB.4L5.B1: marker, mech_sep=-sepTAXNB;
VTAXNB.4L5.B2: marker, mech_sep= sepTAXNB*bv_aux;
VTAXNB.4R1.B1: marker, mech_sep= sepTAXNB;
VTAXNB.4R1.B2: marker, mech_sep=-sepTAXNB*bv_aux;
VTAXNB.4R5.B1: marker, mech_sep= sepTAXNB;
VTAXNB.4R5.B2: marker, mech_sep=-sepTAXNB*bv_aux;


l.TCTPXV=1; l.TCTPHX=1;

TCTPXV: collimator, l:=l.TCTPXV;
TCTPHX: collimator, l:=l.TCTPHX;
TCTPXV.4L1.B1: TCTPXV, mech_sep=-sepTCTPXV;
TCTPXV.4L5.B1: TCTPXV, mech_sep=-sepTCTPXV;
TCTPXV.4R1.B2: TCTPXV, mech_sep=-sepTCTPXV*bv_aux;
TCTPXV.4R5.B2: TCTPXV, mech_sep=-sepTCTPXV*bv_aux;
TCTPXH.4L1.B1: TCTPHX, mech_sep=-sepTCTPXH;
TCTPXH.4L5.B1: TCTPHX, mech_sep=-sepTCTPXH;
TCTPXH.4R1.B2: TCTPHX, mech_sep=-sepTCTPXH*bv_aux;
TCTPXH.4R5.B2: TCTPHX, mech_sep=-sepTCTPXH*bv_aux;

VTCTPXV.4L1.A.B2: marker, mech_sep= sepTCTPXV*bv_aux;
VTCTPXV.4L5.A.B2: marker, mech_sep= sepTCTPXV*bv_aux;
VTCTPXV.4R1.A.B1: marker, mech_sep= sepTCTPXV;
VTCTPXV.4R5.A.B1: marker, mech_sep= sepTCTPXV;
VTCTPXH.4L1.A.B2: marker, mech_sep= sepTCTPXH*bv_aux;
VTCTPXH.4L5.A.B2: marker, mech_sep= sepTCTPXH*bv_aux;
VTCTPXH.4R1.A.B1: marker, mech_sep= sepTCTPXH;
VTCTPXH.4R5.A.B1: marker, mech_sep= sepTCTPXH;

VTCTPXV.4L1.B.B2: marker, mech_sep= sepTCTPXV*bv_aux;
VTCTPXV.4L5.B.B2: marker, mech_sep= sepTCTPXV*bv_aux;
VTCTPXV.4R1.B.B1: marker, mech_sep= sepTCTPXV;
VTCTPXV.4R5.B.B1: marker, mech_sep= sepTCTPXV;
VTCTPXH.4L1.B.B2: marker, mech_sep= sepTCTPXH*bv_aux;
VTCTPXH.4L5.B.B2: marker, mech_sep= sepTCTPXH*bv_aux;
VTCTPXH.4R1.B.B1: marker, mech_sep= sepTCTPXH;
VTCTPXH.4R5.B.B1: marker, mech_sep= sepTCTPXH;


TCTPH.6L1.B1: TCTPH, mech_sep=-0.194;
TCTPV.6L1.B1: TCTPV, mech_sep=-0.194;
TCTPH.6R1.B2: TCTPH, mech_sep=-0.194*bv_aux;
TCTPV.6R1.B2: TCTPV, mech_sep=-0.194*bv_aux;
TCTPH.6L5.B1: TCTPH, mech_sep=-0.194;
TCTPV.6L5.B1: TCTPV, mech_sep=-0.194;
TCTPH.6R5.B2: TCTPH, mech_sep=-0.194*bv_aux;
TCTPV.6R5.B2: TCTPV, mech_sep=-0.194*bv_aux;


l.BPTXaux=0.285;
posBPTX=posTCLMB.4C-1.5/2-0.301-l.BPTXaux/2;
posBPTX=165;


APWL.4L1.B1: APWL, mech_sep=-0.194;
APWL.4L1.B2: APWL, mech_sep= 0.194*bv_aux;
APWL.4L5.B1: APWL, mech_sep=-0.194;
APWL.4L5.B2: APWL, mech_sep= 0.194*bv_aux;
APWL.4R1.B1: APWL, mech_sep= 0.194;
APWL.4R1.B2: APWL, mech_sep=-0.194*bv_aux;
APWL.4R5.B1: APWL, mech_sep= 0.194;
APWL.4R5.B2: APWL, mech_sep=-0.194*bv_aux;

L.APWLaux=0.6;
posAPWL1=165.102; ! temporary B1 P1
posAPWL2=164.502; ! temporary B2 P1
posAPWL3=164.600 ; ! temporary B1 P5
posAPWL4=164.000 ; ! temporary B2 P5

exec,install_ir15_b1_b2_alt4(APWL.4,APWL.4,posAPWL1,posAPWL2,posAPWL3,posAPWL4);

exec,install_ir15_cmn(TAXN.4,posTAXN);
exec,install_ir15_b1_b2(VTAXNA.4,posTAXNA,posTAXNA);
exec,install_ir15_b1_b2(VTAXNB.4,posTAXNB,posTAXNB);

if(bv_aux == 1){
seqedit,sequence=lhcb1;
!! ... TCTPH
install,element=TCTPXH.4L1.B1, at= -posTCTPXH,   from= IP1.L1;
install,element=TCTPXH.4L5.B1, at= -posTCTPXH,   from= IP5;
install,element=VTCTPXH.4R1.A.B1, at= posTCTPXHA,   from= IP1;
install,element=VTCTPXH.4R5.A.B1, at= posTCTPXHA,   from= IP5;
install,element=VTCTPXH.4R1.B.B1, at= posTCTPXHB,   from= IP1;
install,element=VTCTPXH.4R5.B.B1, at= posTCTPXHB,   from= IP5;
install,element=TCTPH.6L1.B1, at= -posTCTPH.6C,   from= IP1.L1;
install,element=TCTPH.6L5.B1, at= -posTCTPH.6Q,   from= IP5;
!! ... TCTPV
install,element=TCTPXV.4L1.B1, at= -posTCTPXV, from=IP1.L1;
install,element=TCTPXV.4L5.B1, at= -posTCTPXV, from=IP5;
install,element=VTCTPXV.4R1.A.B1, at= posTCTPXVA, from=IP1;
install,element=VTCTPXV.4R5.A.B1, at= posTCTPXVA, from=IP5;
install,element=VTCTPXV.4R1.B.B1, at= posTCTPXVB, from=IP1;
install,element=VTCTPXV.4R5.B.B1, at= posTCTPXVB, from=IP5;
install,element=TCTPV.6L1.B1, at= -posTCTPXV.6C,   from= IP1.L1;
install,element=TCTPV.6L5.B1, at= -posTCTPXV.6Q,   from= IP5;
! ... TCL
install,element=TCLPX.4R1.B1,  at= posTCLPX, from=IP1;
install,element=TCLPX.4R5.B1,  at= posTCLPX, from=IP5;
install,element=VTCLPX.4L1.A.B1,  at= -posTCLPXA, from=IP1.L1;
install,element=VTCLPX.4L5.A.B1,  at= -posTCLPXA, from=IP5;
install,element=VTCLPX.4L1.B.B1,  at= -posTCLPXB, from=IP1.L1;
install,element=VTCLPX.4L5.B.B1,  at= -posTCLPXB, from=IP5;
install,element=TCL.5R1.B1,  at= posTCL.5C, from=IP1;
install,element=TCL.5R5.B1,  at= posTCL.5Q, from=IP5;
install,element=TCL.6R1.B1,  at= posTCL.6C, from=IP1;
install,element=TCL.6R5.B1,  at= posTCL.6Q, from=IP5;
! ... BPTX
install,element=BPTX.4L1.B1, at= -posBPTX, from=IP1.L1;
install,element=BPTX.4L5.B1, at= -posBPTX, from=IP5;
endedit;
};

seqedit,sequence=lhcb2;
! ... TCTPH
install,element=VTCTPXH.4L1.A.B2, at= -posTCTPXHA*bv_aux,   from= IP1.L1;
install,element=VTCTPXH.4L5.A.B2, at= -posTCTPXHA*bv_aux,   from= IP5;
install,element=VTCTPXH.4L1.B.B2, at= -posTCTPXHB*bv_aux,   from= IP1.L1;
install,element=VTCTPXH.4L5.B.B2, at= -posTCTPXHB*bv_aux,   from= IP5;
install,element=TCTPXH.4R1.B2, at=  posTCTPXH*bv_aux,   from= IP1;
install,element=TCTPXH.4R5.B2, at=  posTCTPXH*bv_aux,   from= IP5;
install,element=TCTPH.6R1.B2, at=  posTCTPH.6Q*bv_aux,   from= IP1;
install,element=TCTPH.6R5.B2, at=  posTCTPH.6C*bv_aux,   from= IP5;
! ... TCTPV
install,element=TCTPXV.4R1.B2, at= posTCTPXV*bv_aux, from=IP1;
install,element=TCTPXV.4R5.B2, at= posTCTPXV*bv_aux, from=IP5;
install,element=VTCTPXV.4L1.A.B2, at= -posTCTPXVA*bv_aux, from=IP1.L1;
install,element=VTCTPXV.4L5.A.B2, at= -posTCTPXVA*bv_aux, from=IP5;
install,element=VTCTPXV.4L1.B.B2, at= -posTCTPXVB*bv_aux, from=IP1.L1;
install,element=VTCTPXV.4L5.B.B2, at= -posTCTPXVB*bv_aux, from=IP5;
install,element=TCTPV.6R1.B2, at= posTCTPXV.6Q*bv_aux, from=IP1;
install,element=TCTPV.6R5.B2, at= posTCTPXV.6C*bv_aux, from=IP5;
! ... TCL
install,element=TCLPX.4L1.B2,  at= -posTCLPX*bv_aux, from=IP1.L1;
install,element=TCLPX.4L5.B2,  at= -posTCLPX*bv_aux, from=IP5;
install,element=VTCLPX.4R1.A.B2,  at= posTCLPXA*bv_aux, from=IP1;
install,element=VTCLPX.4R5.A.B2,  at= posTCLPXA*bv_aux, from=IP5;
install,element=VTCLPX.4R1.B.B2,  at= posTCLPXB*bv_aux, from=IP1;
install,element=VTCLPX.4R5.B.B2,  at= posTCLPXB*bv_aux, from=IP5;
install,element=TCL.5L1.B2,  at= -posTCL.5Q*bv_aux, from=IP1.L1;
install,element=TCL.5L5.B2,  at= -posTCL.5C*bv_aux, from=IP5;
install,element=TCL.6L1.B2,  at= -posTCL.6Q*bv_aux, from=IP1.L1;
install,element=TCL.6L5.B2,  at= -posTCL.6C*bv_aux, from=IP5;
! ... BPTX
install,element=BPTX.4R1.B2, at= posBPTX*bv_aux, from=IP1;
install,element=BPTX.4R5.B2, at= posBPTX*bv_aux, from=IP5;

!------
! BBLR
!-------

l.BBLR=3.5;
BBLR: placeholder, l=l.BBLR;
posBBLRA=188.07;
posBBLRB=192.87;

exec,newel_ir15_b12(BBLR.4,BBLR,arcSEP);
exec,install_ir15_b1_b2_alt(BBLR.4,posBBLRB,posBBLRA);





! -------
! Install MBH
! -------

mbh_rm_mcs=0; mbh_rm_mcdo=0; !=1 to remove spools
mbh_ir2q8=0;mbh_ir2q10=0;
mbh_ir7q8a=0; mbh_ir7q8b=0;
mbh_ir7q9a=1; mbh_ir7q9b=0;
mbh_ir7q10a=0; mbh_ir7q10b=0;
mbh_ir1q8=0;mbh_ir1q9=0;mbh_ir1q10=0;
mbh_ir5q8=0;mbh_ir5q9=0;mbh_ir5q10=0;

!call,file="slhc/toolkit/install_mbh.madx";


l.MBH                = 5.307; !28/11/2017 F. Savary
Kmax_MBH             = 11.23;
Kmin_MBH             = 0.2460;!..to be reviewed
Imax_MBH             = 11850;
l.tcld              = 1;



!mbh_rm_mcs=0; ! =1 remove mcs
!mbh_rm_mcdo=0; ! =1 remove mco,mcd

MBH: RBEND, L := l.MBH, Kmax := Kmax_MBH, Kmin := Kmin_MBH,
    Calib := Kmax_MBH / Imax_MBH;

tcld: collimator, L=l.tcld;

if (mylhcbeam>2){
  bv_aux=-1;
} else {
  bv_aux=1;
};



install_mbh(mbm,mbn,irn,ira,irb): macro={
print, text="installing MBH in Qmbn  IRirn in the ARCirairb";

abh.airnirb:=ab.airnirb/2;   kbh.airnirb:=abh.airnirb/l.mbh;
abh.airairn:=ab.airairn/2;   kbh.airairn:=abh.airairn/l.mbh;

!tolerances from DS MB Q13-Q7
mbh_shift=-(l.MBH/ab.a78*2*(1/cos(ab.a78/4)-1)+(0.7225*0.0051/4)); !sagitta + drift
g_MBH=0.017150;
r_MBH:=0.022;
r_tol_MBH=0.0009;
h_tol_MBH=0.0008;
v_tol_MBH=0.0005;


if (irn==7){
mech_sep_mbh_lirnb1:= 0.194+mbh_shift*2;
mech_sep_mbh_rirnb1:= 0.194+mbh_shift*2;
mech_sep_mbh_lirnb2:=-0.194+mbh_shift*2;
mech_sep_mbh_rirnb2:=-0.194+mbh_shift*2;
};

if (irn==2){
mech_sep_mbh_lirnb1:= 0.194+mbh_shift*2;
mech_sep_mbh_rirnb1:=-0.194+mbh_shift*2;
mech_sep_mbh_lirnb2:=-0.194+mbh_shift*2;
mech_sep_mbh_rirnb2:= 0.194+mbh_shift*2;
};

if (irn==1 || irn==5){
mech_sep_mbh_lirnb1:=-0.194+mbh_shift*2;
mech_sep_mbh_rirnb1:= 0.194+mbh_shift*2;
mech_sep_mbh_lirnb2:= 0.194+mbh_shift*2;
mech_sep_mbh_rirnb2:=-0.194+mbh_shift*2;
};


mbh.ambnrirn.b1:mbh, angle:= abh.airnirb, k0:= kbh.airnirb,  polarity=+1, mech_sep:=mech_sep_mbh_rirnb1;
mbh.bmbnrirn.b1:mbh, angle:= abh.airnirb, k0:= kbh.airnirb,  polarity=+1, mech_sep:=mech_sep_mbh_rirnb1;
mbh.ambnlirn.b2:mbh, angle:=-abh.airairn, k0:=-kbh.airairn,  polarity=+1, mech_sep:=mech_sep_mbh_lirnb2;
mbh.bmbnlirn.b2:mbh, angle:=-abh.airairn, k0:=-kbh.airairn,  polarity=+1, mech_sep:=mech_sep_mbh_lirnb2;
mbh.ambnrirn.b2:mbh, angle:=-abh.airnirb, k0:=-kbh.airnirb,  polarity=+1, mech_sep:=mech_sep_mbh_rirnb2;
mbh.bmbnrirn.b2:mbh, angle:=-abh.airnirb, k0:=-kbh.airnirb,  polarity=+1, mech_sep:=mech_sep_mbh_rirnb2;
mbh.ambnlirn.b1:mbh, angle:= abh.airairn, k0:= kbh.airairn,  polarity=+1, mech_sep:=mech_sep_mbh_lirnb1;
mbh.bmbnlirn.b1:mbh, angle:= abh.airairn, k0:= kbh.airairn,  polarity=+1, mech_sep:=mech_sep_mbh_lirnb1;





value,mbh_hsag,g_mbh,r_mbh;

mbh.ambnrirn.b1,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.bmbnrirn.b1,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.ambnlirn.b2,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.bmbnlirn.b2,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.ambnrirn.b2,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.bmbnrirn.b2,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.ambnlirn.b1,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};
mbh.bmbnlirn.b1,APERTYPE=RECTELLIPSE,APERTURE:={r_MBH,g_MBH,r_MBH,r_MBH},APER_TOL={r_tol_MBH,h_tol_MBH,v_tol_MBH};



tcld.mbnrirn.b1: tcld;
tcld.mbnlirn.b2: tcld;
vtcld.mbnlirn.b1: tcld;
vtcld.mbnrirn.b2: tcld;



s.tcld.mbnrirn.b1: marker;
s.tcld.mbnlirn.b2: marker;
e.tcld.mbnrirn.b1: marker;
e.tcld.mbnlirn.b2: marker;

pos_mbr=mb.mbmmbnrirn.b1->at;
pos_mbl=mb.mbmmbnlirn.b1->at;

value, pos_mbr, pos_mbl;

if(bv_aux == 1){
Option, -echo,warn,info;
seqedit,sequence=lhcb1;
remove,element=mb.mbmmbnrirn.b1;
remove,element=mb.mbmmbnlirn.b1;
if (mbh_rm_mcs == 1){
remove,element=mcs.mbmmbnrirn.b1;
remove,element=mcs.mbmmbnlirn.b1;
};
if (mbh_rm_mcdo == 1){
remove,element=mco.mbnlirn.b1;
remove,element=mco.mbnrirn.b1;
remove,element=mco.mbnlirn.b1;
remove,element=mco.mbnrirn.b1;
remove,element=mcd.mbnlirn.b1;
remove,element=mcd.mbnrirn.b1;
remove,element=mcd.mbnlirn.b1;
remove,element=mcd.mbnrirn.b1;
};
remove,element=mbh.ambnrirn.b1;
remove,element=mbh.ambnlirn.b1;
remove,element=s.tcld.mbnrirn.b1;
remove,element=tcld.mbnrirn.b1  ;
remove,element=e.tcld.mbnrirn.b1;
remove,element=mbh.bmbnrirn.b1   ;
remove,element=mbh.bmbnlirn.b1   ;
remove,element=vtcld.mbnrirn.b1;
install,element=mbh.ambnlirn.b1  ,at=pos_mbl-4.454,from=ipirn;
install,element=vtcld.mbnrirn.b1 ,at=pos_mbl    ,from=ipirn;
install,element=mbh.bmbnlirn.b1  ,at=pos_mbl+4.454,from=ipirn;
install,element=mbh.ambnrirn.b1  ,at=pos_mbr-4.454,from=ipirn;
install,element=s.tcld.mbnrirn.b1,at=pos_mbr-0.5,from=ipirn;
install,element=tcld.mbnrirn.b1  ,at=pos_mbr    ,from=ipirn;
install,element=e.tcld.mbnrirn.b1,at=pos_mbr+0.5,from=ipirn;
install,element=mbh.bmbnrirn.b1  ,at=pos_mbr+4.454,from=ipirn;
endedit;
};

pos_mbl=mb.mbmmbnlirn.b2->at;
pos_mbr=mb.mbmmbnrirn.b2->at;

seqedit,sequence=lhcb2;
remove,element=mb.mbmmbnlirn.b2;
remove,element=mb.mbmmbnrirn.b2;
if (__mbmbm>0){
remove,element=mcs.mbmmbnrirn.b2;
remove,element=mcs.mbmmbnlirn.b2;
};
if (__mbmbm==1){
remove,element=mco.mbnlirn.b2;
remove,element=mco.mbnrirn.b2;
remove,element=mco.mbnlirn.b2;
remove,element=mco.mbnrirn.b2;
remove,element=mcd.mbnlirn.b2;
remove,element=mcd.mbnrirn.b2;
remove,element=mcd.mbnlirn.b2;
remove,element=mcd.mbnrirn.b2;
};
remove,element=mbh.ambnlirn.b2;
remove,element=mbh.ambnrirn.b2;
remove,element=s.tcld.mbnlirn.b2;
remove,element=s.tcld.mbnrirn.b2;
remove,element=tcld.mbnlirn.b2  ;
remove,element=tcld.mbnrirn.b2  ;
remove,element=e.tcld.mbnlirn.b2;
remove,element=e.tcld.mbnrirn.b2;
remove,element=mbh.bmbnlirn.b2   ;
remove,element=mbh.bmbnrirn.b2   ;
remove,element=vtcld.mbnrirn.b2;
install,element=mbh.ambnlirn.b2,  at=pos_mbl-bv_aux*4.454,from=ipirn;
install,element=s.tcld.mbnlirn.b2,at=pos_mbl-bv_aux*0.5,from=ipirn;
install,element=tcld.mbnlirn.b2  ,at=pos_mbl           ,from=ipirn;
install,element=e.tcld.mbnlirn.b2,at=pos_mbl+bv_aux*0.5,from=ipirn;
install,element=mbh.bmbnlirn.b2,  at=pos_mbl+bv_aux*4.454,from=ipirn;
install,element=mbh.ambnrirn.b2,  at=pos_mbr-bv_aux*4.454,from=ipirn;
install,element=vtcld.mbnrirn.b2, at=pos_mbr           ,from=ipirn;
install,element=mbh.bmbnrirn.b2,  at=pos_mbr+bv_aux*4.454,from=ipirn;
endedit;
Option, -echo,-warn,-info;

!show,mbh.ambnrirn.b1;
!show,mbh.ambnlirn.b2;

};



if (mbh_ir1q8 >0){ exec,install_mbh(b,8 ,1,8,2); };
if (mbh_ir1q9 >0){ exec,install_mbh(b,9 ,1,8,2); };
if (mbh_ir1q10>0){ exec,install_mbh(b,10,1,8,2); };

if (mbh_ir2q8 >0){ exec,install_mbh(a,8 ,2,1,3); };
if (mbh_ir2q10>0){ exec,install_mbh(a,10,2,1,3); };

if (mbh_ir5q8 >0){ exec,install_mbh(b,8 ,5,4,6); };
if (mbh_ir5q9 >0){ exec,install_mbh(b,9 ,5,4,6); };
if (mbh_ir5q10>0){ exec,install_mbh(b,10,5,4,6); };

if (mbh_ir7q8 >0){ exec,install_mbh(b,8 ,7,6,8); };
if (mbh_ir7q10>0){ exec,install_mbh(b,10,7,6,8); };


if (mbh_ir7q8a >0){ exec,install_mbh(a,8 ,7,6,8); };
if (mbh_ir7q8b >0){ exec,install_mbh(b,8 ,7,6,8); };
if (mbh_ir7q9a >0){ exec,install_mbh(a,9 ,7,6,8); };
if (mbh_ir7q9b >0){ exec,install_mbh(b,9 ,7,6,8); };
if (mbh_ir7q10a >0){ exec,install_mbh(a,10 ,7,6,8); };
if (mbh_ir7q10b >0){ exec,install_mbh(b,10 ,7,6,8); };


!end slhc/toolkit/install_mbh.madx

!set default lag for cavities

LAGRF400.B1=0.5;
LAGRF400.B2=0.;

!-----
!IR7 MQW
!-----

if(bv_aux ==1){
seqedit,sequence=lhcb1;
remove,element=mqwa.c5l7.b1;
remove,element=mqwa.d5l7.b1;
remove,element=mqwa.e5l7.b1;
remove,element=mqwb.5l7.b1;
remove,element=mqwa.c5r7.b1;
remove,element=mqwa.d5r7.b1;
remove,element=mqwa.e5r7.b1;
remove,element=mqwb.5r7.b1;
endedit;
};

seqedit,sequence=lhcb2;
remove,element=mqwa.c5l7.b2;
remove,element=mqwa.d5l7.b2;
remove,element=mqwa.e5l7.b2;
remove,element=mqwb.5l7.b2;
remove,element=mqwa.c5r7.b2;
remove,element=mqwa.d5r7.b2;
remove,element=mqwa.e5r7.b2;
remove,element=mqwb.5r7.b2;
endedit;

if(bv_aux ==1){
seqedit,sequence=lhcb1;
install,element=mqwa.c5l7.b1,at=-130.966,from=IP7;
install,element=mqwa.d5l7.b1,at=-134.766,from=IP7;
install,element=mqwa.e5l7.b1,at=-138.566,from=IP7;
install,element=mqwa.c5r7.b1,at= 130.966,from=IP7;
install,element=mqwa.d5r7.b1,at= 134.766,from=IP7;
install,element=mqwa.e5r7.b1,at= 138.566,from=IP7;
endedit;
}

seqedit,sequence=lhcb2;
install,element=mqwa.c5l7.b2,at=-130.966*bv_aux,from=IP7;
install,element=mqwa.d5l7.b2,at=-134.766*bv_aux,from=IP7;
install,element=mqwa.e5l7.b2,at=-138.566*bv_aux,from=IP7;
install,element=mqwa.c5r7.b2,at= 130.966*bv_aux,from=IP7;
install,element=mqwa.d5r7.b2,at= 134.766*bv_aux,from=IP7;
install,element=mqwa.e5r7.b2,at= 138.566*bv_aux,from=IP7;
endedit;


!-----
!IR2 TCLD, TDIS
!-----
! https://indico.cern.ch/event/680374/9/12/2017
! https://indico.cern.ch/event/680374/contributions/2787337/attachments/1570745/2477819/ICL_Meeting_-_11TTCLD_-_CCTCLD_v.3.pptx

posTCLD.2=425.71735;
value,posTCLD.2;


TCLD.11R2.B1: TCLD;
TCLD.11L2.B2: TCLD;


if(bv_aux ==1){
  seqedit,sequence=lhcb1;
  remove,element=TCLD.11R2.B1;
  remove,element=LECL.11R2.B1;
  install,element=TCLD.11R2.B1,  at=posTCLD.2,from=ip2;
  endedit;
};

seqedit,sequence=lhcb2;
remove,element=TCLD.11L2.B2;
remove,element=LEBR.11L2.B2;
install,element=TCLD.11L2.B2,  at=-posTCLD.2*bv_aux,from=ip2;
endedit;

l.TDIS               = 5.74; ! all three modules together
posTDIS = 78.293+l.TDIS/2;
TDIS : collimator, L := l.TDIS;
TDIS.4L2.B1:TDIS;
TDIS.4R8.B2:TDIS;

value,posTDIS;

if(bv_aux ==1){
  seqedit,sequence=lhcb1;
  remove,element=TDI.4L2.B1;
  remove,element=TDIS.4L2.B1;
  install,element=TDIS.4L2.B1,  at=-posTDIS,from=ip2;
  endedit;
};

seqedit,sequence=lhcb2;
remove,element=TDI.4R8.B2;
remove,element=TDIS.4R8.B2;
install,element=TDIS.4R8.B2,  at= posTDIS*bv_aux,from=ip8;
endedit;





!-----
!IR8 TANB
!https://edms.cern.ch/ui/file/1970117/0.1/LHC-TANB-EC-0001-0-1.pdf
!-----
l.TANB=0.605;
posTANB=119.4855;
posBPMWB.8=114.8305;
TANB: collimator,  L := l.TANB;

TANB.4L8.B1: TANB; TANB.4L8.B1: TANB;
TANB.4R8.B2: TANB; TANB.4R8.B2: TANB;

value,posTANB;

if(bv_aux ==1){
  seqedit,sequence=lhcb1;
  remove,element=TANB.4L8.B1;
  remove,element=TANB.4R8.B1;
  remove,element=BPMWB.4L8.B1;
  remove,element=BPMWB.4R8.B1;
  install,element=TANB.4L8.B1,  at=-posTANB,from=ip8;
  install,element=TANB.4R8.B1,  at= posTANB,from=ip8;
  install,element=BPMWB.4L8.B1, at=-posBPMWB.8,from=ip8;
  install,element=BPMWB.4R8.B1, at= posBPMWB.8,from=ip8;
  endedit;
};

seqedit,sequence=lhcb2;
remove,element=TANB.4L8.B2;
remove,element=TANB.4R8.B2;
 remove,element=BPMWB.4L8.B2;
 remove,element=BPMWI.4R8.B2;
install,element=TANB.4L8.B2,  at=-posTANB*bv_aux,from=ip8;
install,element=TANB.4R8.B2,  at= posTANB*bv_aux,from=ip8;
install,element=BPMWB.4L8.B2, at=-posBPMWB.8*bv_aux,from=ip8;
install,element=BPMWI.4R8.B2, at= posBPMWB.8*bv_aux,from=ip8;
endedit;



!-----
!IR4 BGV, BSRT
!-----

posBGV = 200;
posBSRTD4 = 117.114;

BGV.7L4.B2: marker; BGV.7R4.B1: marker;
BSRTD4.5L4.B1: marker; BSRTD4.5R4.B2: marker;

if(bv_aux ==1){
seqedit,sequence=lhcb1;
remove,element=BGV.7R4.B1;
remove,element=BSRTD4.5L4.B1;
endedit;
}

seqedit,sequence=lhcb2;
remove,element=BGV.7L4.B2;
remove,element=BSRTD4.5R4.B2;
endedit;

if(bv_aux ==1){
seqedit,sequence=lhcb1;
install,element=BGV.7R4.B1, at=posBGV, from=IP4;
install,element=BSRTD4.5L4.B1, at=-posBSRTD4, from=IP4;
endedit;
}

seqedit,sequence=lhcb2;
install,element=BGV.7L4.B2, at=-posBGV*bv_aux, from=IP4;
install,element=BSRTD4.5R4.B2, at= posBSRTD4, from=IP4;
endedit;

ELENS.5L4.B1: marker;
ELENS.5L4.B2: marker;
ELENS.5R4.B1: marker;
ELENS.5R4.B2: marker;

if(bv_aux ==1){
  seqedit,sequence=lhcb1;
  remove,element=ELENS.5L4.B1;
  remove,element=ELENS.5R4.B1;
  install,element=ELENS.5L4.B1,  at=-40,from=ip4;
  install,element=ELENS.5R4.B1,  at= 40,from=ip4;
  endedit;
};

seqedit,sequence=lhcb2;
remove,element=ELENS.5L4.B2;
remove,element=ELENS.5R4.B2;
install,element=ELENS.5L4.B2,  at=-40*bv_aux,from=ip4;
install,element=ELENS.5R4.B2,  at= 40*bv_aux,from=ip4;
endedit;

