scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99;
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;
if (q5l6_lim==0) { q5l6_lim=160*7.5/7;};
if (q5r6_lim==0) { q5r6_lim=160*7.5/7;};
qtlimit5l := sch*q5l6_lim/scale;
qtlimit5r := sch*q5r6_lim/scale;


if(on_holdselect==0){ exec,select(6,56,67,b2); };

if(on_fixedip==1){ !V6.503 nominal injection/collision parameter at the IP
muxip6b2 = 2.0000 ;betxip6b2= 187.749224;alfxip6b2 = 0.551968;
muyip6b2 = 2.0300 ;betyip6b2= 178.368556;alfyip6b2 =-0.607183;
dxip6b2 =  0.0;dpxip6b2 = 0 ;!value HLLHCV1.0/HLLHCV1.1: dxip6b2 =  0.186;
};
value,muxip6b2,betxip6b2,alfxip6b2;
value,muyip6b2,betyip6b2,alfyip6b2;
value,dxip6b2,dpxip6b2;

!constants needed for calculation of the constraint 6 (2017) from Chiara 8/6/17
chi6_nsig = 10.1;
chi6_dPoverP = 2e-4;
chi6_emitx=2.5e-6/(7000/0.9382720814);
chi6_maxorbitdrift = 0.6e-3;
al_dump=761;

use,sequence=lhcb2,range=s.ds.l6.b2/e.ds.r6.b2;
match,use_macro;
 vary,name=kqt13.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l6b2, step=1.0E-6, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
 vary,name=kq10.l6b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.l6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq5.l6b2,    step=1.0E-6, lower=-qtlimit5l, upper=-qtlimit5l*scmin;
 vary,name=kq4.l6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;

!vary,name=kq4.r6b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;!fixed for septum
 vary,name=kq5.r6b2,    step=1.0E-6, lower= qtlimit5r*scmin, upper= qtlimit5r;
 vary,name=kq8.r6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.r6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq10.r6b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kqtl11.r6b2, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
 vary,name=kqt12.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
m:macro={

twiss,beta0=bir6b2;

refbetxip6b2=table(twiss,IP6,betx); refalfxip6b2=table(twiss,IP6,alfx);
refbetyip6b2=table(twiss,IP6,bety); refalfyip6b2=table(twiss,IP6,alfy);
refdxip6b2=table(twiss,IP6,dx); refdpxip6b2=table(twiss,IP6,dpx);

refdmuxkickb2_tcsg=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCSP.A4L6.B2,mux);
refdmuxkickb2_tcdqc=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.C4L6.B2,mux);
refdmuxkickb2_tcdqb=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.B4L6.B2,mux);
refdmuxkickb2_tcdqa=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.A4L6.B2,mux);
refdmuxkickb2=refdmuxkickb2_tcdqc;
if(abs(refdmuxkickb2_tcdqa-0.25)>abs(refdmuxkickb2-0.25)){
refdmuxkickb2=refdmuxkickb2_tcdqa;};
if(abs(refdmuxkickb2_tcdqb-0.25)>abs(refdmuxkickb2-0.25)){
refdmuxkickb2=refdmuxkickb2_tcdqb;};

refdxq5r6b2=abs(table(twiss,MQY.5R6.B2,dx));
refdxq4l6b2=abs(table(twiss,MQY.4L6.B2,dx));

refdxtcdqb2=table(twiss,TCDQA.A4L6.B2,dx);
refbetxtcdqb2=table(twiss,TCDQA.A4L6.B2,betx);
refbetytcdqb2=table(twiss,TCDQA.A4L6.B2,bety);
refbetxtcdsb2=table(twiss,TCDSA.4R6.B2,betx);
refbetytcdsb2=table(twiss,TCDSA.4R6.B2,bety);
refbetxtcsgb2=table(twiss,TCSP.A4L6.B2,betx);
refbetytcsgb2=table(twiss,TCSP.A4L6.B2,bety);
refbetxmkdb2 =table(twiss,MKD.H5R6.B2,betx);
refbetymkdb2 =table(twiss,MKD.H5R6.B2,bety);

refbxdumpb2=refbetxip6b2+2*al_dump*refalfxip6b2+
                        al_dump^2*(1+refalfxip6b2^2)/refbetxip6b2;
refbydumpb2=refbetyip6b2+2*al_dump*refalfyip6b2+
                        al_dump^2*(1+refalfyip6b2^2)/refbetyip6b2;
refbdumpb2=sqrt(refbxdumpb2*refbydumpb2);

refdmuxkickb2_bds=table(twiss,MKD.O5R6.B2,mux);
refdmuxkickb2_bdsa=table(twiss,MKD.A5R6.B2,mux);

reftcdqmingapb2 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb2)-3e-4
                 -abs(refdxtcdqb2*chi6_dPoverP)-chi6_maxorbitdrift;
reftcdqgapb2 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb2);
}; !endmacro


if(abs(dmuxkickb2_tcsg)>0){
constraint,weight=1000,expr=refdmuxkickb2_tcsg<dmuxkickb2_tcsg;
} else{
constraint,weight=1000,expr= abs(refdmuxkickb2-0.250)<0.0111;!90+-4 deviation
};
if(bxdumpb2>0 && bydumpb2>0){
constraint,expr=refbxdumpb2=bxdumpb2;
constraint,expr=refbydumpb2=bydumpb2;
} else {
constraint,expr=refbxdumpb2>4000;
constraint,expr=refbydumpb2>3200;
constraint,expr=refbdumpb2>4500;
};
!constraint,sequence=lhcb2,range=MKD.H5R6.B2,betx>380;!380: nom. LHC optics, minimize kick(mkd)/sigma_x for asynch. dump
if(betxtcdqb2>0){
  constraint,expr=refbetxtcdqb2>betxtcdqb2;
}else{
  constraint,sequence=lhcb2,range=TCDQA.A4L6.B2,betx>430;
};
if(betytcdsb2>0){
  constraint,expr=refbetytcdsb2>betytcdsb2;
}else{
  constraint,sequence=lhcb2,range=TCDSA.4R6.B2,bety>200;
};
if(betytcdqb2>0){
  constraint,expr=refbetytcdqb2> betytcdqb2;
} else {
  constraint, sequence=lhcb2,range=TCDQA.A4L6.B2,bety >145;
};
constraint, sequence=lhcb1,range=TCDSA.4R6.B2,bety > 170;
if (NRJ<451){constraint, sequence=lhcb1,range=TCDSA.4R6.B2,betx < 175; };

!dispersion in straight section <0.5
constraint,expr=refdxq5r6b2<1.0; !was 0.5
constraint,expr=refdxq4l6b2<0.5; !was 0.5
constraint,expr=refdxtcdqb2<0.5;  !was 0.5
constraint,sequence=lhcb2,range=MQY.5L6.B2,bety<1500;
constraint,sequence=lhcb2,range=MQY.4R6.B2,bety<1100;

if (dmuxkickb2_bds>0){ constraint,expr=refdmuxkickb2_bds=dmuxkickb2_bds;};

!constraint,sequence=lhcb2,range=ip6,betx=betxip6b2;
!constraint,sequence=lhcb2,range=ip6,bety=betyip6b2;
!constraint,sequence=lhcb2,range=ip6,alfx=alfxip6b2;
!constraint,sequence=lhcb2,range=ip6,alfy=alfyip6b2;
!constraint,sequence=lhcb2,range=ip6,dx=dxip6b2;
!constraint,sequence=lhcb2,range=ip6,dpx=dpxip6b2;

constraint,sequence=lhcb2,range=e.ds.r6.b2,alfx=eir6b2->alfx,alfy=eir6b2->alfy;
constraint,sequence=lhcb2,range=e.ds.r6.b2,betx=eir6b2->betx,bety=eir6b2->bety;
constraint,sequence=lhcb2,range=e.ds.r6.b2,dx=eir6b2->dx,dpx=eir6b2->dpx;
constraint,sequence=lhcb2,range=e.ds.r6.b2,   mux=muxip6b2+eir6b2->mux;
constraint,sequence=lhcb2,range=e.ds.r6.b2,   muy=muyip6b2+eir6b2->muy;
if (betir>0){
constraint,sequence=lhcb2,range=MQY.4L6.B2,   betx <betir;
constraint,sequence=lhcb2,range=MQY.4R6.B2,   bety <betir;
constraint,sequence=lhcb2,range=MQY.5R6.B2,   betx <betir;
constraint,sequence=lhcb2,range=mqml.8l6.b2,betx  < betir;
constraint,sequence=lhcb2,range=mqml.8r6.b2,bety  < betir;
constraint,sequence=lhcb2,range=mqm.9l6.b2,bety   < betir;
constraint,sequence=lhcb2,range=mqm.9r6.b2,betx   < betir;
constraint,sequence=lhcb2,range=mqml.10l6.b2,betx < betir;
constraint,sequence=lhcb2,range=mqml.10r6.b2,bety < betir;
!constraint,sequence=lhcb2,range=MQY.5L6.B2,   bety <677;
!constraint,sequence=lhcb2,range=tcdqm.b4r6.b2,bety <603;
!constraint,sequence=lhcb2,range=tcdqm.b4l6.b2,betx <590;
};
if(match_on_aperture==1){
constraint,range=mq.11l6.b2,bety< 173+epsap;
!!constraint,range= mqml.10l6.b2,betx< 175.2+epsap;
!!constraint,range=mqm.9l6.b2,bety< 180+epsap;
constraint,range=MQY.5L6.B2,   bety<590  +7.2+2.5;
constraint,range=MQY.4L6.B2,   betx<673  +2.0+2.5;
constraint,range=MQY.4R6.B2,   bety<665  +2.0+2.5;
constraint,range=MQY.5R6.B2,  betx<603.1+2.0+2.5;
constraint,range= mcbv.12r6.b2,bety< 174.2+epsap;
constraint,range= mqml.10r6.b2,bety< 176.6+epsap;
!!constraint,range= mqml.10l6.b2,betx< 175.2+epsap;
};
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
endmatch;

exec,mk_irtwiss(6,b2);



value,kq4.r6b2+0.004833837730000;
value,refdmuxkickb2;
value,refdmuxkickb2_tcsg;
value,refdmuxkickb2_tcdq;
value,refbetxtcdqb2,refbetytcdqb2,refbetxtcdsb2,refbetytcdsb2,refbetxtcsgb2,refbetytcsgb2;
value,refdxtcdqb2;
value,refbxdumpb2,refbydumpb2,refbdumpb2;

value,refdmuxkickb2_bds;
value,refmuxip6b2;
value,refmuyip6b2;

betxip6b2=refbetxip6b2 ;
betyip6b2=refbetyip6b2 ;
alfxip6b2=refalfxip6b2 ;
alfyip6b2=refalfyip6b2 ;
dxip6b2 =refdxip6b2 ;
dpxip6b2=refdpxip6b2 ;

value,kq5.l6b2*scale;
value,kq5.r6b2*scale;

tarir6b2=tar;
value,kqt13.l6b2,kqt12.l6b2,kqtl11.l6b2,kq10.l6b2,kq9.l6b2,kq8.l6b2,kq5.l6b2,kq4.l6b2,
kq4.r6b2,kq5.r6b2,kq8.r6b2,kq9.r6b2,kq10.r6b2,kqtl11.r6b2,kqt12.r6b2,kqt13.r6b2;
value,tarir6b2;

!strategy for 11->22 : use all (kq4.r6b2 fixed),alfas,dx as injection,dpx=0,betas not constrained
!strategy for 22->28 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained
!strategy for 22->82 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained (around 320/180)


