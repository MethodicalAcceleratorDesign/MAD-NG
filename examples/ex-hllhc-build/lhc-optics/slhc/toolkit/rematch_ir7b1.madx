

scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimitx28 := 1.0*225.0/scale;
qtlimitx15 := 1.0*205.0/scale;
qtlimit2 := 1.0*160.0/scale;
qtlimit3 := 1.0*200.0/scale;
qtlimit4 := 1.0*125.0/scale;
qtlimit5 := 1.0*120.0/scale;
qtlimit6 := 1.0*90.0/scale;


if(on_holdselect==0){ exec,select(7,67,78,b1); };

if(on_fixedip==1){
muxip7b1 = 2.4550 ;betxip7b1 = 120.8132523 ;alfxip7b1 = 1.276976960 ;
muyip7b1 = 1.9700 ;betyip7b1 = 149.4304704 ;alfyip7b1 =-1.385146304 ;
dxip7b1  = -0.20384709034 ;dpxip7b1 = 0.000 ;
};

value,muxip7b1,betxip7b1,alfxip7b1;
value,muyip7b1,betyip7b1,alfyip7b1;
value,dxip7b1,dpxip7b1;


use,sequence=lhcb1,range=s.ds.l7.b1/e.ds.r7.b1;
match,      sequence=lhcb1, beta0=bir7b1;
weight,mux=10,muy=10;
constraint, sequence=lhcb1, range=ip7,dx=dxip7b1,dpx =dpxip7b1;
constraint, sequence=lhcb1, range=ip7,betx=betxip7b1,bety=betyip7b1;
constraint, sequence=lhcb1, range=ip7,alfx=alfxip7b1,alfy=alfyip7b1;
constraint, sequence=lhcb1, range=e.ds.r7.b1,alfx=eir7b1->alfx,alfy=eir7b1->alfy;
constraint, sequence=lhcb1, range=e.ds.r7.b1,betx=eir7b1->betx,bety=eir7b1->bety;
constraint, sequence=lhcb1, range=e.ds.r7.b1,dx=eir7b1->dx,dpx=eir7b1->dpx;
constraint, sequence=lhcb1, range=e.ds.r7.b1,   mux=muxip7b1+eir7b1->mux;
constraint, sequence=lhcb1, range=e.ds.r7.b1,   muy=muyip7b1+eir7b1->muy;
if(match_on_aperture==1){
constraint, sequence=lhcb1,range=MQ.11l7.b1, bety<180.49-0.33;
constraint, sequence=lhcb1,range=MQ.9l7.b1, bety<174.5;
constraint, sequence=lhcb1,range=MQ.8r7.b1, bety<176.92;
constraint, sequence=lhcb1,range=MQ.10r7.b1, bety<179;
};
vary, name=kqt13.l7b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqt12.l7b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqtl11.l7b1, step=1.0E-9, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
vary, name=kqtl10.l7b1, step=1.0E-9, lower=-qtlimit4*500./550., upper=qtlimit4*500./550.;
vary, name=kqtl9.l7b1,  step=1.0E-9, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
vary, name=kqtl8.l7b1,  step=1.0E-9, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
vary, name=kqtl7.l7b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kq6.l7b1,    step=1.0E-9, lower=-qtlimit6, upper=qtlimit6;
vary, name=kq6.r7b1,    step=1.0E-9, lower=-qtlimit6, upper=qtlimit6;
vary, name=kqtl7.r7b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl8.r7b1,  step=1.0E-9, lower=-qtlimit4*550./550., upper=qtlimit4*550./550.;
vary, name=kqtl9.r7b1,  step=1.0E-9, lower=-qtlimit4*500./550., upper=qtlimit4*500./550.;
vary, name=kqtl10.r7b1, step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl11.r7b1, step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqt12.r7b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqt13.r7b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;-
!lmdif,calls=200,tolerance=1.e-21;
endmatch;

exec,mk_irtwiss(7,b1);
value,KQ4.LR7-0.131382724100E-02,KQT4.L7-0.331689344000E-03,KQT4.R7-0.331689344000E-03,
      KQ5.LR7+0.133553657300E-02,KQT5.L7+0.326577060000E-04,KQT5.R7+0.326577060000E-04;

if(on_aperture==1){
exec,plot_ap(7,b1,$nrj,$full);
};


tarir7b1=tar;
value,tarir7b1;
kq5.lr7=kq5.lr7;
kq4.lr7=kq4.lr7;
kqt5.l7=kqt5.l7;
kqt4.l7=kqt4.l7;
kqt4.r7=kqt4.r7;
kqt5.r7=kqt5.r7;

value,kq5.lr7,kq4.lr7,kqt5.l7,kqt4.l7,kqt4.r7,kqt5.r7,
      kqt13.l7b1,kqt12.l7b1,kqtl11.l7b1,kqtl10.l7b1,kqtl9.l7b1,kqtl8.l7b1,kqtl7.l7b1,kq6.l7b1,
      kq6.r7b1,kqtl7.r7b1,kqtl8.r7b1,kqtl9.r7b1,kqtl10.r7b1,kqtl11.r7b1,kqt12.r7b1,kqt13.r7b1;


