
scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimitx28 := 1.0*225.0/scale;
qtlimitx15 := 1.0*205.0/scale;
qtlimit2 := 1.0*160.0/scale;
qtlimit3 := 1.0*200.0/scale;
qtlimit4 := 1.0*125.0/scale;
qtlimit5 := 1.0*120.0/scale;
qtlimit6 := 1.0*90.0/scale;


if(on_holdselect==0){ exec,select(3,23,34,b1); };

if(on_fixedip==1){
muxip3b1 = 2.2550 ;betxip3b1 = 121.5668445 ;alfxip3b1 = 2.295730634 ;
muyip3b1 = 1.9550 ;betyip3b1 = 218.5850602 ;alfyip3b1 =-2.642890432 ;
dxip3b1  = -0.5105084814 ;dpxip3b1 = -0.006525206629 ;
};

value,muxip3b1,betxip3b1,alfxip3b1;
value,muyip3b1,betyip3b1,alfyip3b1;
value,dxip3b1,dpxip3b1;


use,sequence=lhcb1,range=s.ds.l3.b1/e.ds.r3.b1;
match,      sequence=lhcb1, beta0=bir3b1;
weight,mux=10,muy=10;
constraint, sequence=lhcb1, range=ip3,dx=dxip3b1,dpx =dpxip3b1;
constraint, sequence=lhcb1, range=ip3,betx=betxip3b1,bety=betyip3b1;
constraint, sequence=lhcb1, range=ip3,alfx=alfxip3b1,alfy=alfyip3b1;
constraint, sequence=lhcb1, range=e.ds.r3.b1,alfx=eir3b1->alfx,alfy=eir3b1->alfy;
constraint, sequence=lhcb1, range=e.ds.r3.b1,betx=eir3b1->betx,bety=eir3b1->bety;
constraint, sequence=lhcb1, range=e.ds.r3.b1,dx=eir3b1->dx,dpx=eir3b1->dpx;
constraint, sequence=lhcb1, range=e.ds.r3.b1,   mux=muxip3b1+eir3b1->mux;
constraint, sequence=lhcb1, range=e.ds.r3.b1,   muy=muyip3b1+eir3b1->muy;
if(match_on_aperture==1){
constraint, sequence=lhcb1,range=MQ.11l3.b1, bety<184.4+epsap;
constraint, sequence=lhcb1,range=MQ.9l3.b1, bety<178.7+epsap;
constraint, sequence=lhcb1,range=MQ.8r3.b1, bety<185.5+epsap;
constraint, sequence=lhcb1,range=MQ.10r3.b1, bety<170+epsap;
constraint, sequence=lhcb1,range=MQ.11r3.b1, betx<184.95+epsap;
};
vary, name=kqt13.l3b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqt12.l3b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqtl11.l3b1, step=1.0E-9, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
vary, name=kqtl10.l3b1, step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl9.l3b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl8.l3b1,  step=1.0E-9, lower=-qtlimit4*450./550., upper=qtlimit4*450./550.;
vary, name=kqtl7.l3b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kq6.l3b1,    step=1.0E-9, lower=-qtlimit6, upper=qtlimit6;
vary, name=kq6.r3b1,    step=1.0E-9, lower=-qtlimit6, upper=qtlimit6;
vary, name=kqtl7.r3b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl8.r3b1,  step=1.0E-9, lower=-qtlimit4, upper=qtlimit4;
vary, name=kqtl9.r3b1,  step=1.0E-9, lower=-qtlimit4*500./550., upper=qtlimit4*500./550.;
vary, name=kqtl10.r3b1, step=1.0E-9, lower=-qtlimit4*450./550., upper=qtlimit4*450./550.;
vary, name=kqtl11.r3b1, step=1.0E-9, lower=-qtlimit4*520./550., upper=qtlimit4*520./550.;
vary, name=kqt12.r3b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;
vary, name=kqt13.r3b1,  step=1.0E-9, lower=-qtlimit5, upper=qtlimit5;

jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;
!lmdif,calls=200,tolerance=1.e-21;
endmatch;

exec,mk_irtwiss(3,b1);

if(on_aperture==1){
exec,plot_ap(3,b1,$nrj,$full);
};

tarir3b1=tar;

kq4.lr3=kq4.lr3;
kqt4.l3=kqt4.l3;
kqt4.r3=kqt4.r3;
kq5.lr3=kq5.lr3;
kqt5.l3=kqt5.l3;
kqt5.r3=kqt5.r3;

value,kq5.lr3,kq4.lr3,kqt5.l3,kqt4.l3,kqt4.r3,kqt5.r3,
kqt13.l3b1,kqt12.l3b1,kqtl11.l3b1,
kqtl10.l3b1,kqtl9.l3b1,kqtl8.l3b1,kqtl7.l3b1,kq6.l3b1,
kq6.r3b1,kqtl7.r3b1,kqtl8.r3b1,kqtl9.r3b1,kqtl10.r3b1,
kqtl11.r3b1,kqt12.r3b1,kqt13.r3b1;
value,tarir3b1;
