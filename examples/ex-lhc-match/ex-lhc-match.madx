! time ../madx64 ex-lhc-match.madx > ex-lhc-matchmdx.out 2>&1

! Links definitions
option, warn,info;
set, format="22.16e";

option, -echo,-warn,-info;

call, file="lhc_as-built.seq";
call, file="opt_400_10000_400_3000.madx";

option, warn;

beam, sequence=lhcb1, energy=nrj, particle=proton;
use, sequence=lhcb1;

select,flag=twiss,clear;
select,flag=twiss, pattern=ip, column=s, name, betx, bety, alfx, alfy;
twiss,chrom,table, file="lhcb1_twiss_x.tfs";

!!!!!!!!!!!!!!!!!!!!
print, text="STARTING TUNE MATCHING";
system, "date";
ncall1=0;

match, use_macro;
 vary, name=kqtf.b1, step=1E-7 ;
 vary, name=kqtd.b1, step=1E-7 ;
 m1: macro = {
   ncall1 = ncall1+1 ;
   twiss ;
   value, table(summ,q1), table(summ,q2) ;
 }
 constraint, expr = table(summ,q1)=64.295 ;
 constraint, expr = table(summ,q2)=59.301 ;
 jacobian, calls=100, tolerance=1E-20 ;
 !lmdif, calls=100, tolerance=1E-20 ;
endmatch ;

print, text="END OF TUNE MATCHING";
system, "date";
value, ncall1;

!!!!!!!!!!!!!!!!!!!!
print, text="STARTING CHROMA MATCHING";
system, "date";
ncall2=0;

match, use_macro ;
 vary, name=ksf.b1, step=1E-7 ;
 vary, name=ksd.b1, step=1E-7 ;
 m1: macro = {
   ncall2 = ncall2+1 ;
   twiss, chrom ;
   value, table(summ,dq1), table(summ,dq2) ;
 }
 constraint, expr = table(summ,dq1)=15 ;
 constraint, expr = table(summ,dq2)=15 ;
 jacobian, calls=100, tolerance=1E-16 ;
 !lmdif, calls=100, tolerance=1E-20 ;
endmatch ;

print, text="END OF CHROMA MATCHING";
system, "date";
value, ncall1, kqtf.b1, kqtd.b1;
value, ncall2, ksf.b1 , ksd.b1 ;

exit;

!!!!!!!!!!!!!!!!!!!!
MADX results: LMDIF
ncall1             =  8.0000000000000000e+00 ;
kqtf.b1            = -7.0604422285532785e-05 ;
kqtd.b1            =  7.8038884481505303e-05 ;
ncall2             =  1.1000000000000000e+01 ;
ksf.b1             =  1.3096499820771104e-01 ;
ksd.b1             = -2.1692283846966087e-01 ;
real  0m24.404s

MADX results: JACOBIAN (bisec=3)
FAILS
