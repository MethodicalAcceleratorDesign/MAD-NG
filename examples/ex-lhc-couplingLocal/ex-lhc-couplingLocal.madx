! TAKES ABOUT 3MIN, GENERATES 1800 TRACKING FILES!

Option, -echo,-warn,-info;
call,file="lhc_as-built.seq";
call,file="opticsfile.21";
Option, echo,warn,info;

value, NRJ;
beam, sequence=lhcb1, energy=NRJ, particle=proton;
use, sequence=lhcb1;

select, flag=twiss, clear;
select, flag=twiss, PATTERN=BPM, column= name,s,beta11, beta12, alfa11, alfa12, mux, muy, betx, bety, dx, dy, r12,r21,r22,r11;
select, flag=twiss, PATTERN=IP, column= name,s,beta11, beta12,alfa11, alfa12, mux, muy, betx, bety, dx, dy, r12,r21,r22,r11;
select, flag=twiss, PATTERN=MQXA, column= name,s,beta11, beta12,alfa11, alfa12, mux, muy, betx, bety, dx, dy, r12,r21,r22,r11;

KTQX1.R2    =             -KTQX1.L2 ; ! remove the link between these 2 vars

kqsx3.l2 = -0.0015;
kqsx3.r2 = +0.0015;

twiss, sequence=lhcb1, file="before_tune_correction_x.tfs", RIPKEN, chrom;

MATCH, SEQUENCE=lhcb1;
VARY, NAME=dQx.b1, STEP=1.0E-5;
VARY, NAME=dQy.b1, STEP=1.0E-5;
CONSTRAINT, SEQUENCE=lhcb1, RANGE=#e, MUX=62.3098, MUY=60.321539;
LMDIF, CALLS=100, TOLERANCE=0.000001;
endmatch;

twiss, sequence=lhcb1, file="after_tune_correction_x.tfs", RIPKEN, chrom;

ptc_create_universe;
ptc_create_layout,model=3,method=4,nst=3;
option, -info;
n = 1;
while (n <= 900) {
	Nsigmax=tgauss(5);
	Nsigmay=tgauss(5);
	Nsigmapx=tgauss(5);
	Nsigmapy=tgauss(5);
	printf, text="%03g % -.5f % -.5f % -.5f % -.5f", value=n, Nsigmax, Nsigmay, Nsigmapx, Nsigmapy;

	enomphysy=4e-6/(6500/0.938);
	enomphysx=6e-6/(6500/0.938);

	betxip1=0.5;
	betyip1=betxip1;

	myx=sqrt(betxip1)*Nsigmax*sqrt(enomphysx);
	myy=sqrt(betyip1)*Nsigmay*sqrt(enomphysy);
	mypx=Nsigmapx*sqrt(enomphysx)/sqrt(betxip1);
	mypy=Nsigmapy*sqrt(enomphysy)/sqrt(betyip1);
	ptc_start, x=myx, px=mypx, y=myy, py=mypy;
	n=n+1;
};
option, info;

ptc_observe, place=ip2;
ptc_track,icase=5,dump,turns=10,element_by_element; !,closed_orbit !,norm_no=5;
ptc_track_end;
ptc_end;

removefile, file="internal_mag_pot.txt";

